
// Форматирует сумму прописью документа
//
// Параметры:
//  СуммаДок - число - реквизит, который надо представить прописью 
//  СуммаБезКопеек - булево - флаг представления суммы без копеек
//
// Возвращаемое значение
//  Отформатированную строку
//
Функция ФорматироватьСуммуПрописи(СуммаДок,СуммаБезКопеек)
	
	Результат     = СуммаДок;
	ЦелаяЧасть    = Цел(СуммаДок);
	ФорматСтрока  = "Л=uk_UA; ДП=Ложь";
	ПарамПредмета = СсылкаНаОбъект.ВалютаДокумента.ПараметрыПрописиНаУкраинском;
	
	Если (Результат - ЦелаяЧасть) = 0 Тогда
		Если СуммаБезКопеек Тогда
			Результат = ЧислоПрописью(Результат,ФорматСтрока,ПарамПредмета);
			Результат = Лев(Результат,Найти(Результат,"0") - 1) + "=";
		Иначе
			Результат = ЧислоПрописью(Результат,ФорматСтрока,ПарамПредмета);
		КонецЕсли;
	Иначе
		Результат = ЧислоПрописью(Результат,ФорматСтрока,ПарамПредмета);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ФорматироватьСуммуПрописи()

// Форматирует сумму  документа
//
// Параметры:
//  СуммаДок - число - реквизит, который надо отформатировать
//  СуммаБезКопеек - булево - флаг представления суммы без копеек
//
// Возвращаемое значение
//  Отформатированную строку
//
Функция ФорматироватьСумму(СуммаДок,СуммаБезКопеек)
	
	Результат = Формат(СуммаДок,"ЧДЦ=2; ЧРД=,; ЧГ=0");
	
	Возврат Результат;
	
КонецФункции // ФорматироватьСумму()
	
// Формирует печатную форму 
// платежного поручения
//
// Параметры:
//  ТабДок - табличный документ
//
// Серна - Начало
Функция ПечатьПлатежногоПоручения(ТабДокумент = Неопределено) Экспорт
// Серна - Конец
	мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

	Если СсылкаНаОбъект.Организация.Пустая() Тогда
		Сообщить(НСтр("ru='Не указана организация.';uk='Не зазначена організація.'"), СтатусСообщения.Важное);
		Возврат Неопределено;
	КонецЕсли;

	Если СсылкаНаОбъект.Контрагент.Пустая() И Не СсылкаНаОбъект.ВидОперации=Перечисления.ВидыОперацийППИсходящее.ПереводНаДругойСчет Тогда
		Сообщить(НСтр("ru='Не указан контрагент.';uk='Не зазначений контрагент.'"), СтатусСообщения.Важное);
		Возврат Неопределено;
	КонецЕсли;
	
	Если СсылкаНаОбъект.СчетОрганизации.Пустая() Тогда
		Сообщить(НСтр("ru='Не указан расчетный счет.';uk='Не зазначений розрахунковий рахунок.'"), СтатусСообщения.Важное);
		Возврат Неопределено;
	КонецЕсли;
	
    Если СсылкаНаОбъект.СчетОрганизации.ВалютаДенежныхСредств <> мВалютаРегламентированногоУчета Тогда
		Сообщить(НСтр("ru='Для документа в валюте предусмотрена другая печатная форма.';uk='Для документа у валюті передбачена інша друкована форма.'"), СтатусСообщения.Важное);
		Возврат Неопределено;	
	КонецЕсли;

	
// Серна - Начало
	Если ТабДокумент = Неопределено Тогда
		ТабДокумент = Новый ТабличныйДокумент;
	КонецЕсли;
// Серна - Конец
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПлатежноеПоручение_ПлатежноеПоручение";
	
	Макет = ПолучитьОбщийМакет("ПлатежноеПоручение_2009");
	
	КодЯзыкаПечать = "uk";
	
	Обл   = Макет.ПолучитьОбласть("Шапка");
	
	СуммаБезКопеек  = СсылкаНаОбъект.СчетОрганизации.СуммаБезКопеек;
	
	БанкОрганизации = СсылкаНаОбъект.СчетОрганизации.Банк;
	БанкКонтрагента = СсылкаНаОбъект.СчетКонтрагента.Банк;
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(СсылкаНаОбъект.Организация, СсылкаНаОбъект.ДатаВходящегоДокумента);
	
	Обл.Параметры.НомерПП					= СсылкаНаОбъект.НомерВходящегоДокумента;
	
	ЧислоДокумента                          = Формат(СсылкаНаОбъект.ДатаВходящегоДокумента,"ДФ='дд';Л=uk_UA");
	ДатаДокумента                           = Формат(СсылкаНаОбъект.ДатаВходящегоДокумента,"ДФ='дд ММММ ггггр.';Л=uk_UA");
	ДатаДокументаБезЧисла                   = Прав(ДатаДокумента, СтрДлина(ДатаДокумента) - 2);
	Обл.Параметры.ДатаДокумента          	= """" + ЧислоДокумента + """" + ДатаДокументаБезЧисла;
	
	Обл.Параметры.СуммаПлатежа             	= ФорматироватьСумму(СсылкаНаОбъект.СуммаДокумента,СуммаБезКопеек);
	Обл.Параметры.СуммаПрописью          	= ФорматироватьСуммуПрописи(СсылкаНаОбъект.СуммаДокумента,СуммаБезКопеек);

	Обл.Параметры.Плательщик              = СсылкаНаОбъект.Контрагент.НаименованиеПолное;
	Обл.Параметры.КодПоЕДРПОУ 			  = СсылкаНаОбъект.Контрагент.КодПоЕДРПОУ;
	Обл.Параметры.БанкПлательщика         = БанкКонтрагента;
    Обл.Параметры.КодБанкаПлательщика     = БанкКонтрагента.Код;
	лНомерСчета = СокрЛП(СсылкаНаОбъект.СчетКонтрагента.НомерСчета);
	Если ВРег(Лев(лНомерСчета,2)) = "UA" Тогда
		лНомерСчета = Прав(лНомерСчета, СтрДлина(лНомерСчета) - 10);
		Если Лев(лНомерСчета,1) = "0" Тогда
			Пока Лев(лНомерСчета,1) = "0" Цикл
				лНомерСчета = Прав(лНомерСчета, СтрДлина(лНомерСчета) - 1);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
  	Обл.Параметры.НомерСчетаПлательщика   = лНомерСчета;
	
	Обл.Параметры.Получатель              = СсылкаНаОбъект.Организация.НаименованиеПолное;
	Обл.Параметры.ПолучательЕДРПОУ        = УправлениеКонтактнойИнформацией.ПолучитьКодОрганизации(СведенияОбОрганизации);
    Обл.Параметры.БанкПолучателя          = БанкОрганизации;
	Обл.Параметры.КодБанкаПолучателя      = БанкОрганизации.Код;
	лНомерСчета = СокрЛП(СсылкаНаОбъект.СчетОрганизации.НомерСчета);
	Если ВРег(Лев(лНомерСчета,2)) = "UA" Тогда
		лНомерСчета = прав(лНомерСчета, СтрДлина(лНомерСчета) - 10);
		Если Лев(лНомерСчета,1) = "0" Тогда
			Пока Лев(лНомерСчета,1) = "0" Цикл
				лНомерСчета = Прав(лНомерСчета, СтрДлина(лНомерСчета) - 1);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Обл.Параметры.НомерСчетаПолучателя    = лНомерСчета;

	Обл.Параметры.НазначениеПлатежа       = СокрЛП(СсылкаНаОбъект.НазначениеПлатежа);
	
// Серна - Начало
	КолВо = 1;
	//Если Не ВвестиЧисло(КолВо, НСтр("ru='Введите число экземпляров';uk='Введіть число екземплярів'"), 2) Тогда
	//	Возврат Неопределено;	
	//КонецЕсли;	
// Серна - Конец
	
	Для НомерЭкземпляра = 1 по КолВо Цикл
		Обл.Параметры.НомерЭкземпляра = НомерЭкземпляра;
	    ТабДокумент.Вывести(Обл);
		Если  НЕ ТабДокумент.ПроверитьВывод(Обл)  Тогда
		    ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
	КонецЦикла;	

	Возврат ТабДокумент;

КонецФункции // ПечатьПлатежногоПоручения()

Процедура Печать() Экспорт
	ТабДокумент = ПечатьПлатежногоПоручения();
	КоличествоЭкземпляров = 1;
	НепосредственнаяПечать = Ложь;
	НаПринтер = Ложь;
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(СсылкаНаОбъект, СсылкаНаОбъект.Метаданные().Представление()), НепосредственнаяПечать);
КонецПроцедуры // Печать
