Функция Печать() Экспорт
	
	// Для отладки
	лТест = БИТ_МСФО_Серна.СчетМожноИспользоватьВПроводках("1091");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент"	, СсылкаНаОбъект.Ссылка);
	Запрос.УстановитьПараметр("НаДату"          , СсылкаНаОбъект.Дата);
	Запрос.УстановитьПараметр("Организация"     , СсылкаНаОбъект.Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПередачаМалоценныхАктивовВЭксплуатацию.Номер 	                      КАК НомерДокумента,
	|	ПередачаМалоценныхАктивовВЭксплуатацию.Дата	                       	  КАК ДатаДокумента,
	|	ПередачаМалоценныхАктивовВЭксплуатацию.Организация                    КАК Организация,
	|	ПередачаМалоценныхАктивовВЭксплуатацию.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
	|	ПередачаМалоценныхАктивовВЭксплуатацию.Склад                          КАК Склад,
	|	ПередачаМалоценныхАктивовВЭксплуатацию.Автомобиль                     КАК Автомобиль,
	|	ПередачаМалоценныхАктивовВЭксплуатацию.ПробегАвтомобиля				  КАК ПробегАвтомобиля,
	|	КодыОрганизации.КодПоЕДРПОУ    										  КАК КодПоЕДРПОУ,
	|	ПередачаМалоценныхАктивовВЭксплуатацию.ПодразделениеОрганизации 	  КАК ПредставлениеПодразделения
	|ИЗ
	|	Документ.ПередачаМалоценныхАктивовВЭксплуатацию КАК ПередачаМалоценныхАктивовВЭксплуатацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			РегистрСведений.КодыОрганизации.СрезПоследних(
	|			                &НаДату,
	|			                Организация = &Организация) КАК КодыОрганизации
	|		ПО ПередачаМалоценныхАктивовВЭксплуатацию.Организация = КодыОрганизации.Организация
	|
	|ГДЕ
	|	ПередачаМалоценныхАктивовВЭксплуатацию.Ссылка = &ТекущийДокумент";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект.Ссылка);
 	Запрос.УстановитьПараметр("ВидМалоценногоАктива"   , Перечисления.ВидыМалоценныхАктивов.МалоценныйБыстроизнашивающийсяПредмет);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Номенклатура.Код КАК НоменклатурныйНомер,
	|	ВЫРАЗИТЬ(Док.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК МатериалНаименование,
	|	Док.Количество КАК Количество,
	|	Док.ФизЛицо КАК Сотрудник,
	|	Док.ФизЛицо.Код КАК ТабельныйНомер,
	|	Док.ДатаДокумента КАК ДатаДокумента,
	|	Док.СрокСлужбы КАК СрокСлужбы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Док.Номенклатура КАК Номенклатура,
	|		СУММА(Док.Количество) КАК Количество,
	|		МИНИМУМ(Док.НомерСтроки) КАК НомерСтроки,
	|		Док.ФизЛицо КАК ФизЛицо,
	|		Док.Ссылка.Дата КАК ДатаДокумента,
	|		Док.НазначениеИспользования.СрокПолезногоИспользования КАК СрокСлужбы
	|	ИЗ
	|		Документ.ПередачаМалоценныхАктивовВЭксплуатацию.МалоценныеАктивы КАК Док
	|	ГДЕ
	|		Док.Ссылка = &ТекущийДокумент
	|		И Док.НазначениеИспользования.ВидМалоценногоАктива = &ВидМалоценногоАктива
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Док.Номенклатура,
	|		Док.ФизЛицо,
	|		Док.Ссылка.Дата,
	|		Док.НазначениеИспользования.СрокПолезногоИспользования) КАК Док
	|
	|УПОРЯДОЧИТЬ ПО
	|	Док.НомерСтроки
	|ИТОГИ ПО
	|	Сотрудник";

	ЗапросПоНоменклатуре = Запрос.Выполнить();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПередачаМалоценныхАктивовВЭксплуатацию_МШ2";
	
	// Вывод заголовка
	Макет = ПолучитьМакет("МШ2");
	
	Если ЗапросПоНоменклатуре.Пустой() Тогда
		
		ОбластьМакета = Макет.ПолучитьОбласть("НетДанных");
		ТабДокумент.Вывести(ОбластьМакета);
		
		Возврат ТабДокумент;
		
	КонецЕсли;
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Заполнить(Шапка);
	
	НеПервый                 = Ложь;
	
	ВыборкаСотрудники = ЗапросПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Пока ВыборкаСотрудники.Следующий() Цикл
		
		Если НеПервый Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			НеПервый = Истина;
		КонецЕсли;
		
		ВыборкаПоСтрокам = ВыборкаСотрудники.Выбрать();
	
		КоличествоВВыборке     = ВыборкаПоСтрокам.Количество();
		КоличествоОбработанных = 1;
		
		ДанныеСотрудника = ОбщегоНазначения.ДанныеФизЛица(СсылкаНаОбъект.Организация, ВыборкаСотрудники.Сотрудник, СсылкаНаОбъект.Дата);
		
		ОбластьШапка.Параметры.Сотрудник 		= ДанныеСотрудника.Фамилия + " " + ДанныеСотрудника.Имя + " " + ДанныеСотрудника.Отчество;
		ОбластьШапка.Параметры.Должность 		= ДанныеСотрудника.Должность;
		
		ОбластьШапка.Параметры.ТабельныйНомер	= ВыборкаСотрудники.ТабельныйНомер; //ДанныеСотрудника.ТабельныйНомер
	
		ТабДокумент.Вывести(ОбластьШапка);
		ВысотаШапки   = ТабДокумент.ВысотаТаблицы;
		ШиринаТаблицы = ТабДокумент.ШиринаТаблицы;
		ТабДокумент.ПовторятьПриПечатиСтроки = ТабДокумент.Область("R" + ВысотаШапки);
		ТабДокумент.ОбластьПечати            = ТабДокумент.Область("C2:C" + ШиринаТаблицы);
			
		Пока ВыборкаПоСтрокам.Следующий() Цикл

			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.Заполнить(ВыборкаПоСтрокам);
			Область.Параметры.МатериалНаименование = СокрЛП(ВыборкаПоСтрокам.МатериалНаименование);
			
			Если КоличествоОбработанных = КоличествоВВыборке Тогда
				// Последняя запись. Ее не следует отрывать от подписей.
				
				МассивТаблиц = Новый Массив(1);
				МассивТаблиц[0] = Область;
				
				Если НЕ ТабДокумент.ПроверитьВывод(МассивТаблиц) Тогда
					
					ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					
				КонецЕсли;
				
			КонецЕсли;
			
			ТабДокумент.Вывести(Область);
			КоличествоОбработанных = КоличествоОбработанных + 1;

		КонецЦикла;
			
	КонецЦикла;		

	Возврат ТабДокумент;

КонецФункции

Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	МассивНазначений.Добавить("Документ.ПередачаМалоценныхАктивовВЭксплуатацию");
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	//возможны варианты - ЗаполнениеОбъекта, ДополнительныйОтчет, СозданиеСвязанныхОбъектов,
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "Печатная форма МШ-2"); //имя под kt обработка зарегистрирована будет в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", ЛОЖЬ);
	ПараметрыРегистрации.Вставить("Информация", "Печатная форма МШ-2");
	//команды
	ТаблицаКоманд = Новый ТаблицаЗначений;
	ТаблицаКоманд.Колонки.Добавить("Представление"); //как будет выглядеть описание печ.формы для пользователя
	ТаблицаКоманд.Колонки.Добавить("Идентификатор"); //имя нашего макета
	ТаблицаКоманд.Колонки.Добавить("Использование"); //ВызовСерверногоМетода
	ТаблицаКоманд.Колонки.Добавить("ПоказыватьОповещение"); //Истина
	ТаблицаКоманд.Колонки.Добавить("Модификатор"); //ПечатьМХL
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = "Печатная форма МШ-2";
	НоваяКоманда.Идентификатор = "ПечатьМШ2"; //Внешняя печатная форма
	НоваяКоманда.Использование = "ВызовКлиентскогоМетода"; //здесь можно прописать использование как серверного так и клиентского, отличие в том, что серверный метод будет обращаться к экспортной процедуре из модуля объекта, клиентский - к экспортной процедуре из модуля формы объекта
	НоваяКоманда.ПоказыватьОповещение = Истина;
	НоваяКоманда.Модификатор = "ПечатьMXL";
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	Возврат ПараметрыРегистрации;
КонецФункции
