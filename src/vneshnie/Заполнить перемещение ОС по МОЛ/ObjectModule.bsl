// Инициализировать
//
Процедура Инициализировать(Объект, ИмяТабличнойЧасти, ТабличноеПоле) Экспорт
	
	Если Не ИмяТабличнойЧасти = "ОС" Тогда Возврат; КонецЕсли;
	Если ложь Тогда
		Объект = Документы.ПеремещениеОС.СоздатьДокумент();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Сдал) Тогда
		ОбщегоНазначения.СообщитьИнформацию("Не заполнен реквизит 'Сдал' в документе!");
		Возврат;
	КонецЕсли;
	
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("Дата", ?( ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата()));
	лЗапрос.УстановитьПараметр("МОЛ", Объект.Сдал);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	Рег.ОсновноеСредство КАК ОС,
	|	Рег.МОЛ,
	|	Рег.Местонахождение
	|ПОМЕСТИТЬ Филиал
	|ИЗ
	|	РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&Дата, ) КАК Рег
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегБаланс.Счет КАК СчетБаланс,
	|	РегБаланс.Субконто1 КАК ОС,
	|	РегБаланс.СуммаОстаток КАК Баланс
	|ПОМЕСТИТЬ БухИтоги
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновныеСредства))
	|				ИЛИ Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДругиеНеоборотныеМатериальныеАктивыГруппа))
	|				ИЛИ Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НематериальныеАктивы)),
	|			,
	|			) КАК РегБаланс
	|ГДЕ
	|	РегБаланс.СуммаОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухИтоги.ОС КАК ОсновноеСредство
	|ИЗ
	|	БухИтоги КАК БухИтоги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Филиал КАК Филиал
	|		ПО БухИтоги.ОС = Филиал.ОС
	|ГДЕ
	|	ЕСТЬNULL(Филиал.МОЛ, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) = &МОЛ
	|";
	лтзРез = лЗапрос.Выполнить().Выгрузить();
	Объект.ОС.Очистить();
	Объект.ОС.Загрузить( лтзРез );
КонецПроцедуры // Инициализировать
