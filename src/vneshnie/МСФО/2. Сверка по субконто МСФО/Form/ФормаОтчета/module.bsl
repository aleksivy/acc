// УстановитьОтбор
//
Процедура УстановитьОтбор(КомпоновщикНастроек, ИмяПараметра, ЗначениеПараметра)
	ЭлементОтбора = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПараметра);
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
	Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ЗначениеПараметра;	
		Попытка
			лЭтоГруппа = ЗначениеПараметра.ЭтоГруппа;
			Если лЭтоГруппа Тогда
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
			Иначе
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		Исключение
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецПопытки;
	Иначе
		ЭлементОтбора.Использование = Ложь;
	КонецЕсли;
КонецПроцедуры

// УстановитьПараметр
//
Процедура УстановитьПараметр(КомпоновщикНастроек, ИмяПараметра, ЗначениеПараметра)
	ПараметрДанныхТекущий = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	ПараметрДанныхТекущий.Значение = ЗначениеПараметра;
	ПараметрДанныхТекущий.Использование = ЗначениеЗаполнено(ЗначениеПараметра);
КонецПроцедуры

Процедура УстановитьПараметрыОтборы()
	УстановитьПараметр(КомпоновщикНастроек, "Организация", Организация );
	УстановитьПараметр(КомпоновщикНастроек, "Дата", КонецДня(ДатаОтчета) + 1 );
	УстановитьПараметр(КомпоновщикНастроек, "Счет", Счет );
	УстановитьПараметр(КомпоновщикНастроек, "СчетМСФО", СчетМСФО );
КонецПроцедуры

Процедура ПриОткрытии()
	Если Не ЗначениеЗаполнено(Организация) Тогда Организация = глЗначениеПеременной( "ОсновнаяОрганизация" ); КонецЕсли;
	Если Не ЗначениеЗаполнено(ДатаОтчета) Тогда ДатаОтчета = НачалоМесяца(ТекущаяДата())-1; КонецЕсли;
	Если Не ЗначениеЗаполнено(Счет) Тогда Счет = ПланыСчетов.Хозрасчетный.НайтиПоКоду("361"); КонецЕсли;
	Если Не ЗначениеЗаполнено(СчетМСФО) Тогда СчетМСФО = ПланыСчетов.МСФО.НайтиПоКоду("361"); КонецЕсли;
	
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура ДатаОтчетаПриИзменении(Элемент)
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура СчетПриИзменении(Элемент)
	СчетМСФО = ПланыСчетов.МСФО.НайтиПоКоду( Счет.Код );
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура СчетМСФОПриИзменении(Элемент)
	Счет = ПланыСчетов.Хозрасчетный.НайтиПоКоду( СчетМСФО.Код );
	УстановитьПараметрыОтборы();
КонецПроцедуры
