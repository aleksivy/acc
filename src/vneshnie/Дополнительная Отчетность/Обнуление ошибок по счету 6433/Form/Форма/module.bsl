
Функция Запрос3()
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("Дата",  КонецДня(ДатаОстатков) );
	лЗапрос.УстановитьПараметр("ПределСуммы", ПределСуммы);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	Обл.Счет,
	|	Обл.Субконто1,
	|	Обл.Субконто2,
	|	Обл.Субконто3 КАК Субконто3Пустые,
	|	Пустые.Субконто3 КАК Субконто3Обл,
	|	Обл.Организация,
	|	Обл.Валюта,
	|	Обл.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	Пустые.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	ВЫБОР
	|		КОГДА Обл.СуммаОстаток < 0
	|			ТОГДА ВЫБОР
	|					КОГДА -Обл.СуммаОстаток < Пустые.СуммаОстаток
	|						ТОГДА Обл.СуммаОстаток
	|					ИНАЧЕ -Пустые.СуммаОстаток
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Обл.СуммаОстаток < -Пустые.СуммаОстаток
	|					ТОГДА -Обл.СуммаОстаток
	|				ИНАЧЕ Пустые.СуммаОстаток
	|			КОНЕЦ
	|	КОНЕЦ КАК Сумма,
	|	Обл.СуммаОстаток КАК СуммаОбл,
	|	Пустые.СуммаОстаток КАК СуммаПустые
	|ПОМЕСТИТЬ Врем
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговыхОбязательств), , НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)) КАК Обл
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговыхОбязательств), , НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)) КАК Пустые
	|		ПО Обл.Счет = Пустые.Счет
	|			И Обл.Субконто1 = Пустые.Субконто1
	|			И Обл.Субконто2 = Пустые.Субконто2
	|			И (Обл.Субконто3 = Пустые.Субконто3 или Обл.Субконто3 = НЕОПРЕДЕЛЕНО)
	|			И (Обл.СуммаОстаток + Пустые.СуммаОстаток <= &ПределСуммы)
	|			И (Обл.СуммаОстаток + Пустые.СуммаОстаток >= -&ПределСуммы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем.Счет КАК СчетДт,
	|	Врем.Счет КАК СчетКт,
	|	Врем.Субконто1 КАК СубконтоДт1,
	|	Врем.Субконто2 КАК СубконтоДт2,
	|	Врем.Субконто3Обл КАК СубконтоДт3,
	|	Врем.Субконто1 КАК СубконтоКт1,
	|	Врем.Субконто2 КАК СубконтоКт2,
	|	Врем.Субконто3Пустые КАК СубконтоКт3,
	|	Врем.Организация,
	|	Врем.Валюта,
	|	Врем.НалоговоеНазначениеДт,
	|	Врем.НалоговоеНазначениеКт,
	|	Врем.Сумма
	|ИЗ
	|	Врем КАК Врем";
	
	лтзРез = лЗапрос.Выполнить().Выгрузить();
	Возврат лтзРез;
КонецФункции

Функция Запрос2()
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("Дата",  КонецДня(ДатаОстатков) );
	лЗапрос.УстановитьПараметр("ПределСуммы", ПределСуммы);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	Обл.Счет,
	|	Обл.Субконто1,
	|	Обл.Субконто2,
	|	Обл.Организация,
	|	Обл.Валюта,
	|	Обл.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	Пустые.НалоговоеНазначение КАК НалоговоеНазначениеКт
	|ПОМЕСТИТЬ Врем1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговыхОбязательств), , НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)) КАК Обл
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговыхОбязательств), , НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)) КАК Пустые
	|		ПО Обл.Счет = Пустые.Счет
	|			И Обл.Субконто1 = Пустые.Субконто1
	|			И Обл.Субконто2 = Пустые.Субконто2
	|			И (Обл.СуммаОстаток + Пустые.СуммаОстаток <= &ПределСуммы)
	|			И (Обл.СуммаОстаток + Пустые.СуммаОстаток >= -&ПределСуммы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем1.Счет,
	|	Врем1.Субконто1,
	|	Врем1.Субконто2,
	|	Врем1.Организация,
	|	Врем1.Валюта,
	|	Врем1.НалоговоеНазначениеДт,
	|	Врем1.НалоговоеНазначениеКт,
	|	Обл.Субконто3,
	|	Обл.СуммаОстаток
	|ПОМЕСТИТЬ ВремОбл
	|ИЗ
	|	Врем1 КАК Врем1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговыхОбязательств), , НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)) КАК Обл
	|		ПО Врем1.Счет = Обл.Счет
	|			И Врем1.Субконто1 = Обл.Субконто1
	|			И Врем1.Субконто2 = Обл.Субконто2
	|			И Врем1.Организация = Обл.Организация
	|			И Врем1.НалоговоеНазначениеДт = Обл.НалоговоеНазначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем1.Счет,
	|	Врем1.Субконто1,
	|	Врем1.Субконто2,
	|	Врем1.Организация,
	|	Врем1.Валюта,
	|	Врем1.НалоговоеНазначениеДт,
	|	Врем1.НалоговоеНазначениеКт,
	|	Пустая.Субконто3,
	|	Пустая.СуммаОстаток
	|ПОМЕСТИТЬ ВремПустая
	|ИЗ
	|	Врем1 КАК Врем1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговыхОбязательств), , НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)) КАК Пустая
	|		ПО Врем1.Счет = Пустая.Счет
	|			И Врем1.Субконто1 = Пустая.Субконто1
	|			И Врем1.Субконто2 = Пустая.Субконто2
	|			И Врем1.Организация = Пустая.Организация
	|			И Врем1.НалоговоеНазначениеКт = Пустая.НалоговоеНазначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремОбл.Счет,
	|	ВремОбл.Субконто1,
	|	ВремОбл.Субконто2,
	|	ВремОбл.Организация,
	|	ВремОбл.Валюта,
	|	ВремОбл.НалоговоеНазначениеДт,
	|	ВремОбл.НалоговоеНазначениеКт,
	|	ВремОбл.Субконто3 КАК Субконто3Обл,
	|	ВремПустая.Субконто3 КАК Субконто3Пустые,
	|	ВЫБОР
	|		КОГДА ВремОбл.СуммаОстаток < 0
	|			ТОГДА ВЫБОР
	|					КОГДА -ВремОбл.СуммаОстаток < ВремПустая.СуммаОстаток
	|						ТОГДА ВремОбл.СуммаОстаток
	|					ИНАЧЕ -ВремПустая.СуммаОстаток
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВремОбл.СуммаОстаток < -ВремПустая.СуммаОстаток
	|					ТОГДА -ВремОбл.СуммаОстаток
	|				ИНАЧЕ ВремПустая.СуммаОстаток
	|			КОНЕЦ
	|	КОНЕЦ КАК Сумма,
	|	ВремОбл.СуммаОстаток КАК СуммаОбл,
	|	ВремПустая.СуммаОстаток КАК СуммаПустая
	|ПОМЕСТИТЬ Врем
	|ИЗ
	|	ВремОбл КАК ВремОбл
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВремПустая КАК ВремПустая
	|		ПО ВремОбл.Счет = ВремПустая.Счет
	|			И ВремОбл.Субконто1 = ВремПустая.Субконто1
	|			И ВремОбл.Субконто2 = ВремПустая.Субконто2
	|			И ВремОбл.Организация = ВремПустая.Организация
	|			И ВремОбл.НалоговоеНазначениеДт = ВремПустая.НалоговоеНазначениеДт
	|			И ВремОбл.НалоговоеНазначениеКт = ВремПустая.НалоговоеНазначениеКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем.Счет КАК СчетДт,
	|	Врем.Счет КАК СчетКт,
	|	Врем.Субконто1 КАК СубконтоДт1,
	|	Врем.Субконто2 КАК СубконтоДт2,
	|	Врем.Субконто3Обл КАК СубконтоДт3,
	|	Врем.Субконто1 КАК СубконтоКт1,
	|	Врем.Субконто2 КАК СубконтоКт2,
	|	Врем.Субконто3Пустые КАК СубконтоКт3,
	|	Врем.Организация,
	|	Врем.Валюта,
	|	Врем.НалоговоеНазначениеДт,
	|	Врем.НалоговоеНазначениеКт,
	|	Врем.Сумма
	|ИЗ
	|	Врем КАК Врем";
	
	лтзРез = лЗапрос.Выполнить().Выгрузить();
	Возврат лтзРез;
КонецФункции

Функция Запрос1()
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("Дата",  КонецДня(ДатаОстатков) );
	лЗапрос.УстановитьПараметр("ПределСуммы", ПределСуммы);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	Обл.Счет,
	|	Обл.Субконто1,
	|	Обл.Субконто2,
	|	Обл.Субконто3 КАК Субконто3Обл,
	|	Обл.Организация,
	|	Обл.Валюта,
	|	Обл.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	Обл.СуммаОстаток КАК СуммаОбл
	|ПОМЕСТИТЬ Врем1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговыхОбязательств), , НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)) КАК Обл
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пустые.Счет,
	|	Пустые.Субконто1,
	|	Пустые.Субконто2,
	|	Пустые.Субконто3 КАК Субконто3Пустые,
	|	Пустые.Организация,
	|	Пустые.Валюта,
	|	Пустые.НалоговоеНазначение КАК НалоговоеНазначениеКт,
	|	Пустые.СуммаОстаток КАК СуммаПустые
	|ПОМЕСТИТЬ Врем2
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкиНалоговыхОбязательств), , НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка)) КАК Пустые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем2.Счет,
	|	Врем2.Субконто1,
	|	Врем2.Субконто2,
	|	Врем2.Организация,
	|	Врем2.Валюта,
	|	Врем2.НалоговоеНазначениеКт,
	|	СУММА(Врем2.СуммаПустые) КАК СуммаПустыеВсего
	|ПОМЕСТИТЬ Врем2Свернуто
	|ИЗ
	|	Врем2 КАК Врем2
	|
	|СГРУППИРОВАТЬ ПО
	|	Врем2.Счет,
	|	Врем2.Валюта,
	|	Врем2.Организация,
	|	Врем2.Субконто2,
	|	Врем2.Субконто1,
	|	Врем2.НалоговоеНазначениеКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем2.Счет,
	|	Врем2.Субконто1,
	|	Врем2.Субконто2,
	|	Врем2.Субконто3Пустые,
	|	Врем2.Организация,
	|	Врем2.Валюта,
	|	Врем2.НалоговоеНазначениеКт,
	|	Врем2.СуммаПустые,
	|	Врем2Свернуто.СуммаПустыеВсего
	|ПОМЕСТИТЬ Врем22
	|ИЗ
	|	Врем2 КАК Врем2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Врем2Свернуто КАК Врем2Свернуто
	|		ПО Врем2.Счет = Врем2Свернуто.Счет
	|			И Врем2.Субконто1 = Врем2Свернуто.Субконто1
	|			И Врем2.Субконто2 = Врем2Свернуто.Субконто2
	|			И Врем2.Организация = Врем2Свернуто.Организация
	|			И Врем2.НалоговоеНазначениеКт = Врем2Свернуто.НалоговоеНазначениеКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обл.Счет,
	|	Обл.Субконто1,
	|	Обл.Субконто2,
	|	Обл.Субконто3Обл,
	|	Пустые.Субконто3Пустые,
	|	Обл.Организация,
	|	Обл.Валюта,
	|	Обл.НалоговоеНазначениеДт,
	|	Пустые.НалоговоеНазначениеКт,
	|	ВЫБОР
	|		КОГДА Обл.СуммаОбл < 0
	|			ТОГДА ВЫБОР
	|					КОГДА -Обл.СуммаОбл < Пустые.СуммаПустые
	|						ТОГДА Обл.СуммаОбл
	|					ИНАЧЕ -Пустые.СуммаПустые
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Обл.СуммаОбл < -Пустые.СуммаПустые
	|					ТОГДА -Обл.СуммаОбл
	|				ИНАЧЕ Пустые.СуммаПустые
	|			КОНЕЦ
	|	КОНЕЦ КАК Сумма,
	|	Обл.СуммаОбл,
	|	Пустые.СуммаПустые
	|ПОМЕСТИТЬ Врем
	|ИЗ
	|	Врем1 КАК Обл
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Врем22 КАК Пустые
	|		ПО Обл.Счет = Пустые.Счет
	|			И Обл.Субконто1 = Пустые.Субконто1
	|			И Обл.Субконто2 = Пустые.Субконто2
	|			И (Обл.Субконто3Обл = Пустые.Субконто3Пустые
	|				ИЛИ Обл.Субконто3Обл = НЕОПРЕДЕЛЕНО)
	|			И (Обл.СуммаОбл + Пустые.СуммаПустые <= &ПределСуммы
	|				ИЛИ Обл.СуммаОбл + Пустые.СуммаПустыеВсего <= &ПределСуммы)
	|			И (Обл.СуммаОбл + Пустые.СуммаПустые >= -&ПределСуммы
	|				ИЛИ Обл.СуммаОбл + Пустые.СуммаПустыеВсего >= -&ПределСуммы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем.Счет КАК СчетДт,
	|	Врем.Счет КАК СчетКт,
	|	Врем.Субконто1 КАК СубконтоДт1,
	|	Врем.Субконто2 КАК СубконтоДт2,
	|	Врем.Субконто3Обл КАК СубконтоДт3,
	|	Врем.Субконто1 КАК СубконтоКт1,
	|	Врем.Субконто2 КАК СубконтоКт2,
	|	Врем.Субконто3Пустые КАК СубконтоКт3,
	|	Врем.Организация,
	|	Врем.Валюта,
	|	Врем.НалоговоеНазначениеДт,
	|	Врем.НалоговоеНазначениеКт,
	|	Врем.Сумма
	|ИЗ
	|	Врем КАК Врем";
	
	лтзРез = лЗапрос.Выполнить().Выгрузить();
	Возврат лтзРез;
КонецФункции

Процедура КнопкаВыполнитьНажатие(Кнопка)
	Если Не ЗначениеЗаполнено(ДатаОстатков) Тогда Сообщить("Не заполнен реквизит 'Дата остатков'"); Возврат; КонецЕсли;
	//Если Не ЗначениеЗаполнено(ПределСуммы) Тогда Сообщить("Не заполнен реквизит 'Предел суммы'"); Возврат; КонецЕсли;
	Если Не ЗначениеЗаполнено(БухгалтерскаяСправка) Тогда Сообщить("Не заполнен реквизит 'Бухгалтерская cправка'"); Возврат; КонецЕсли;
	Если Не Переформировывать и (БухгалтерскаяСправка.Проводки.Количество() > 0) Тогда Сообщить("Указанная бухгалтерская cправка уже заполнена"); Возврат; КонецЕсли;
	
	лОбъектБухгалтерскаяСправка = БухгалтерскаяСправка.ПолучитьОбъект();
	лОбъектБухгалтерскаяСправка.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	
	Если (лОбъектБухгалтерскаяСправка.Проводки.Количество() > 0) Тогда
		лОбъектБухгалтерскаяСправка.Проводки.Очистить();
	КонецЕсли;
	
	Состояние("Обработка : шаг 1 из 3");
	лтзРез = Запрос1();
	лОбъектБухгалтерскаяСправка.Проводки.Загрузить( лтзРез );
	лОбъектБухгалтерскаяСправка.Записать(РежимЗаписиДокумента.Проведение);
	
	Состояние("Обработка : шаг 2 из 3");
	лтзРез = Запрос2();
	Для каждого лСтрока Из лтзРез Цикл
		лНоваяСтрока = лОбъектБухгалтерскаяСправка.Проводки.Добавить();
		ЗаполнитьЗначенияСвойств(лНоваяСтрока, лСтрока);
	КонецЦикла;
	лОбъектБухгалтерскаяСправка.Записать(РежимЗаписиДокумента.Проведение);
	
	Состояние("Обработка : шаг 3 из 3");
	лтзРез = Запрос3();
	Для каждого лСтрока Из лтзРез Цикл
		лНоваяСтрока = лОбъектБухгалтерскаяСправка.Проводки.Добавить();
		ЗаполнитьЗначенияСвойств(лНоваяСтрока, лСтрока);
	КонецЦикла;
	лОбъектБухгалтерскаяСправка.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры
