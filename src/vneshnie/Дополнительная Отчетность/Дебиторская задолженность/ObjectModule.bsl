Функция СформироватьЗапрос() Экспорт
	
	МассивСчетов = Новый Массив;
	
	Для Каждого СтрокаСчет Из СчетаУчета Цикл 
		Если СтрокаСчет.Пометка Тогда 
			МассивСчетов.Добавить(СтрокаСчет.Счет);
		КонецЕсли;		
	КонецЦикла;
	
	МассивКонтрагентов = Новый Массив;
	
	Для Каждого СтрокаКонтрагент Из Контрагенты Цикл 
		МассивКонтрагентов.Добавить(СтрокаКонтрагент.Контрагент);
	КонецЦикла;
	
	Если БезДоговоров Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
		|	ХозрасчетныйОбороты.Период КАК Период,
		|	ВЫБОР
		|		КОГДА &Дебиторы
		|			ТОГДА ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) - ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0)
		|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0) - ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0)
		|	КОНЕЦ КАК СуммаЗадолженности,
		|	ВЫБОР
		|		КОГДА &Дебиторы
		|			ТОГДА ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотДт, 0) - ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0)
		|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0) - ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотДт, 0)
		|	КОНЕЦ КАК СуммаОборот,
		|	ВЫБОР
		|		КОГДА &Дебиторы
		|			ТОГДА ЕСТЬNULL(ХозрасчетныйОстаткиНачало.СуммаОстатокДт, 0) - ЕСТЬNULL(ХозрасчетныйОстаткиНачало.СуммаОстатокКт, 0)
		|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстаткиНачало.СуммаОстатокКт, 0) - ЕСТЬNULL(ХозрасчетныйОстаткиНачало.СуммаОстатокДт, 0)
		|	КОНЕЦ КАК СуммаРанее
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ДатаКонца,
		|			Счет В ИЕРАРХИИ (&Счета),
		|			,
		|			Организация = &Организация
		|				И (&ПоВсемКонтрагентам
		|					ИЛИ Субконто1 В ИЕРАРХИИ (&Контрагенты))) КАК ХозрасчетныйОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(
		|				&ДатаНачала,
		|				&Дата,
		|				Месяц,
		|				Счет В ИЕРАРХИИ (&Счета),
		|				,
		|				Организация = &Организация
		|					И (&ПоВсемКонтрагентам
		|						ИЛИ Субконто1 В ИЕРАРХИИ (&Контрагенты)),
		|				,
		|				) КАК ХозрасчетныйОбороты
		|		ПО ХозрасчетныйОстатки.Субконто1 = ХозрасчетныйОбороты.Субконто1
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
		|				&ДатаНачала,
		|				Счет В ИЕРАРХИИ (&Счета),
		|				,
		|				Организация = &Организация
		|					И (&ПоВсемКонтрагентам
		|						ИЛИ Субконто1 В ИЕРАРХИИ (&Контрагенты))) КАК ХозрасчетныйОстаткиНачало
		|		ПО ХозрасчетныйОстатки.Субконто1 = ХозрасчетныйОстаткиНачало.Субконто1
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Дебиторы
		|				ТОГДА ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) - ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0)
		|			ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0) - ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0)
		|		КОНЕЦ >= &МинСумма
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагент
		|ИТОГИ
		|	СРЕДНЕЕ(СуммаЗадолженности),
		|	СУММА(СуммаОборот),
		|	СРЕДНЕЕ(СуммаРанее)
		|ПО
		|	Контрагент
		|АВТОУПОРЯДОЧИВАНИЕ
		|";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
		|	ХозрасчетныйОстатки.Субконто2 КАК Договор,
		|	ХозрасчетныйОбороты.Период КАК Период,
		|	ВЫБОР
		|		КОГДА &Дебиторы
		|			ТОГДА ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) - ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0)
		|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0) - ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0)
		|	КОНЕЦ КАК СуммаЗадолженности,
		|	ВЫБОР
		|		КОГДА &Дебиторы
		|			ТОГДА ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотДт, 0) - ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0)
		|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0) - ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотДт, 0)
		|	КОНЕЦ КАК СуммаОборот,
		|	ВЫБОР
		|		КОГДА &Дебиторы
		|			ТОГДА ЕСТЬNULL(ХозрасчетныйОстаткиНачало.СуммаОстатокДт, 0) - ЕСТЬNULL(ХозрасчетныйОстаткиНачало.СуммаОстатокКт, 0)
		|		ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстаткиНачало.СуммаОстатокКт, 0) - ЕСТЬNULL(ХозрасчетныйОстаткиНачало.СуммаОстатокДт, 0)
		|	КОНЕЦ КАК СуммаРанее
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ДатаКонца,
		|			Счет В ИЕРАРХИИ (&Счета),
		|			,
		|			Организация = &Организация
		|				И (&ПоВсемКонтрагентам
		|					ИЛИ Субконто1 В ИЕРАРХИИ (&Контрагенты))) КАК ХозрасчетныйОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(
		|				&ДатаНачала,
		|				&Дата,
		|				Месяц,
		|				Счет В ИЕРАРХИИ (&Счета),
		|				,
		|				Организация = &Организация
		|					И (&ПоВсемКонтрагентам
		|						ИЛИ Субконто1 В ИЕРАРХИИ (&Контрагенты)),
		|				,
		|				) КАК ХозрасчетныйОбороты
		|		ПО ХозрасчетныйОстатки.Субконто1 = ХозрасчетныйОбороты.Субконто1
		|			И ХозрасчетныйОстатки.Субконто2 = ХозрасчетныйОбороты.Субконто2
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
		|				&ДатаНачала,
		|				Счет В ИЕРАРХИИ (&Счета),
		|				,
		|				Организация = &Организация
		|					И (&ПоВсемКонтрагентам
		|						ИЛИ Субконто1 В ИЕРАРХИИ (&Контрагенты))) КАК ХозрасчетныйОстаткиНачало
		|		ПО ХозрасчетныйОстатки.Субконто1 = ХозрасчетныйОстаткиНачало.Субконто1
		|			И ХозрасчетныйОстатки.Субконто2 = ХозрасчетныйОстаткиНачало.Субконто2
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Дебиторы
		|				ТОГДА ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) - ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0)
		|			ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокКт, 0) - ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0)
		|		КОНЕЦ >= &МинСумма
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагент,
		|	Договор
		|ИТОГИ
		|	СРЕДНЕЕ(СуммаЗадолженности),
		|	СУММА(СуммаОборот),
		|	СРЕДНЕЕ(СуммаРанее)
		|ПО
		|	Контрагент,
		|	Договор
		|АВТОУПОРЯДОЧИВАНИЕ
		|";
	КонецЕсли;

	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
	Запрос.УстановитьПараметр("ДатаКонца", КонецДня(Дата)+1);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(ДобавитьМесяц(Дата,-Глубина)));
	Запрос.УстановитьПараметр("ПоВсемКонтрагентам", (МассивКонтрагентов.Количество() = 0));
	Запрос.УстановитьПараметр("Контрагенты", МассивКонтрагентов);
	Запрос.УстановитьПараметр("Организация", Организация);	
	Запрос.УстановитьПараметр("Счета", МассивСчетов);
	Запрос.УстановитьПараметр("Дебиторы", (ТипОтчета = 1));
	Запрос.УстановитьПараметр("МинСумма", МинСумма);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПодготовитьТаблицуПериодов()
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Период");
	ТЗ.Колонки.Добавить("СуммаИтог");
	
	СтрокаНачало = ТЗ.Добавить();
	СтрокаНачало.Период    = НачалоМесяца(Дата);
	СтрокаНачало.СуммаИтог = 0;	
	
	Для Счетчик = 1 По Глубина Цикл 
		СтрокаТЗ = ТЗ.Добавить();
		СтрокаТЗ.Период    = НачалоМесяца(ДобавитьМесяц(Дата,-Счетчик));
		СтрокаТЗ.СуммаИтог = 0;	
	КонецЦикла;

	Возврат ТЗ;
	
КонецФункции

Процедура СформироватьОтчет() Экспорт 
	
	ТаблицаПериодов   = ПодготовитьТаблицуПериодов();
	лРезЗапроса		  = СформироватьЗапрос();
	ВыборкаКонтрагент = лРезЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Макет             = ПолучитьМакет("Макет");	
	Результат         = Новый ТабличныйДокумент;
	СуммаИтогоНачало  = 0;
	СуммаИтогоКонец   = 0;
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.Дата = Формат(Дата, "ДФ = 'дд MMMM гггг'");
	ОбластьЗаголовок.Параметры.КАКАЯ = ?(ТипОтчета = 1,"Дебиторская","Кредиторская");
	СписокСчетов = "";
	
	Для Каждого СтрокаСчет Из СчетаУчета Цикл 
		Если СтрокаСчет.Пометка Тогда 
			СписокСчетов = СписокСчетов+Строка(СтрокаСчет.Счет)+", ";
		КонецЕсли;
	КонецЦикла;
	
	ОбластьЗаголовок.Параметры.СписокСчетов = СписокСчетов;
	
	Результат.Вывести(ОбластьЗаголовок);
	
	ОбластьШапкаНачало = Макет.ПолучитьОбласть("Шапка | Начало");
	Результат.Вывести(ОбластьШапкаНачало);
	
	ОбластьШапкаПериод = Макет.ПолучитьОбласть("Шапка | Период");
	ОбластьШапкаПериод.Параметры.Период = Формат(НачалоМесяца(Дата),"ДФ = дд.MM.гггг")+" - "+Формат(Дата,"ДФ = дд.MM.гггг");
	Результат.Присоединить(ОбластьШапкаПериод);
	
	Для Сч = 1 По ТаблицаПериодов.Количество()-1 Цикл 
		ОбластьШапкаПериод = Макет.ПолучитьОбласть("Шапка | Период");
		ОбластьШапкаПериод.Параметры.Период = Формат(ТаблицаПериодов[Сч].Период ,"ДФ = 'MMMM гггг'")+" г.";
		Результат.Присоединить(ОбластьШапкаПериод);
	КонецЦикла;
	
	ОбластьШапкаПоследнийПериод = Макет.ПолучитьОбласть("Шапка | Конец");
	ОбластьШапкаПоследнийПериод.Параметры.ПоследнийПериод = Формат(НачалоМесяца(ДобавитьМесяц(Дата,-Глубина)),"ДФ = 'MMMM гггг'");
	Результат.Присоединить(ОбластьШапкаПоследнийПериод);
	
	Если БезДоговоров Тогда
		Пока ВыборкаКонтрагент.Следующий() Цикл 
				
				ОбластьСтрокаНачало = Макет.ПолучитьОбласть("Строка | Начало");	
				ОбластьСтрокаНачало.Параметры.Заполнить(ВыборкаКонтрагент);
				СуммаИтогоКонец = СуммаИтогоКонец + ВыборкаКонтрагент.СуммаЗадолженности;
				Результат.Вывести(ОбластьСтрокаНачало);
				
				ВыборкаПериоды = ВыборкаКонтрагент.Выбрать();
				
				Для Каждого СтрокаПериод Из ТаблицаПериодов Цикл 
					
					ОбластьСтрокаПериод = Макет.ПолучитьОбласть("Строка | Период");
					
					Пока ВыборкаПериоды.Следующий() Цикл
						
						Если СтрокаПериод.Период = ВыборкаПериоды.Период Тогда
							ОбластьСтрокаПериод.Параметры.СуммаОборот = ВыборкаПериоды.СуммаОборот;
							СтрокаПериод.СуммаИтог = СтрокаПериод.СуммаИтог + ВыборкаПериоды.СуммаОборот; 
						КонецЕсли;
						
					КонецЦикла;
					
					ВыборкаПериоды.Сбросить();
					Результат.Присоединить(ОбластьСтрокаПериод);
					
				КонецЦикла;
				
				ОбластьСтрокаКонец = Макет.ПолучитьОбласть("Строка | Конец");	
				ОбластьСтрокаКонец.Параметры.Заполнить(ВыборкаКонтрагент);
				СуммаИтогоНачало = СуммаИтогоНачало + ВыборкаКонтрагент.СуммаРанее;
				Результат.Присоединить(ОбластьСтрокаКонец);
		КонецЦикла;
	Иначе
		Пока ВыборкаКонтрагент.Следующий() Цикл 
			ВыборкаДоговор = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДоговор.Следующий() Цикл 
				
				ОбластьСтрокаНачало = Макет.ПолучитьОбласть("Строка | Начало");	
				ОбластьСтрокаНачало.Параметры.Заполнить(ВыборкаДоговор);
				СуммаИтогоКонец = СуммаИтогоКонец + ВыборкаДоговор.СуммаЗадолженности;
				Результат.Вывести(ОбластьСтрокаНачало);
				
				ВыборкаПериоды = ВыборкаДоговор.Выбрать();
				
				Для Каждого СтрокаПериод Из ТаблицаПериодов Цикл 
					
					ОбластьСтрокаПериод = Макет.ПолучитьОбласть("Строка | Период");
					
					Пока ВыборкаПериоды.Следующий() Цикл
						
						Если СтрокаПериод.Период = ВыборкаПериоды.Период Тогда
							ОбластьСтрокаПериод.Параметры.СуммаОборот = ВыборкаПериоды.СуммаОборот;
							СтрокаПериод.СуммаИтог = СтрокаПериод.СуммаИтог + ВыборкаПериоды.СуммаОборот; 
						КонецЕсли;
						
					КонецЦикла;
					
					ВыборкаПериоды.Сбросить();
					Результат.Присоединить(ОбластьСтрокаПериод);
					
				КонецЦикла;
				
				ОбластьСтрокаКонец = Макет.ПолучитьОбласть("Строка | Конец");	
				ОбластьСтрокаКонец.Параметры.Заполнить(ВыборкаДоговор);
				СуммаИтогоНачало = СуммаИтогоНачало + ВыборкаДоговор.СуммаРанее;
				Результат.Присоединить(ОбластьСтрокаКонец);
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ОбластьИтогНачало = Макет.ПолучитьОбласть("Итого | Начало");
	ОбластьИтогНачало.Параметры.СуммаЗадолженности = СуммаИтогоКонец;
	Результат.Вывести(ОбластьИтогНачало);
	
	Для Каждого СтрокаПериод Из ТаблицаПериодов Цикл 
		
		ОбластьИтогПериод = Макет.ПолучитьОбласть("Итого | Период");
		ОбластьИтогПериод.Параметры.СуммаОборот = СтрокаПериод.СуммаИтог;

		Результат.Присоединить(ОбластьИтогПериод);
		
	КонецЦикла;
	
	ОбластьИтогКонец = Макет.ПолучитьОбласть("Итого | Конец");
	ОбластьИтогКонец.Параметры.СуммаРанее = СуммаИтогоНачало;
	Результат.Присоединить(ОбластьИтогКонец);
	
	УниверсальныеМеханизмы.НапечататьДокумент(Результат,,,"Задолженность контрагентов");
	
КонецПроцедуры