Перем ИмяФормата;

// Возвращает Истина, если код в банке есть, Ложь - иначе
//
// Параметры: нет
//
// Возвращаемое значение:
//
//   Булево   – Истина, если код в банке есть, Ложь - иначе
Функция ЕстьКодВБанке() Экспорт
	Возврат Ложь;
КонецФункции	
	
// Возвращает наименование кода в банке
//
// Параметры:
//
//	нет
//
// Возвращаемое значение:
//
// 	Строка - наименование кода
Функция ПолучитьНаименованиеКода() Экспорт
	Возврат "";
КонецФункции		

// Возвращает код банка организации
//
// Параметры:
//
//  Структура Параметры
// 		Ключ Организация - Организация - Справочник.Ссылки.Организация          
//
// Возвращаемое значение:
//
//   Строка   – код организации в банке
Функция кбПолучитьКодОрганизации(Параметры) Экспорт
	Возврат "";
КонецФункции

// Возвращает по имени файла каталог
//
// Параметры:
//
//  Файл - имя файла - строка
//
// Возвращаемое значение:
//
//   Строка – каталог
//
Функция ПолучитьКаталог(Файл)
	
	Если Файл = Неопределено Тогда
		Возврат "";
	КонецЕсли;	
	ИмяФайла = Файл;
	
	Каталог = "";
	поз = Найти(ИмяФайла, "\");
	Пока поз Цикл
		Каталог = Каталог + Лев(ИмяФайла, поз);
		ИмяФайла = Сред(ИмяФайла, поз + 1);
		поз = Найти(ИмяФайла, "\");
	КонецЦикла;	
	Возврат Каталог;
	
КонецФункции	

// Формирует имя файла по умолчанию
//
// Параметры:
//
// 	Структура Параметры
//		Ключ Режим - Режим - Строка "Импорт"/"Экспорт"
// 		Ключ Дата -  Дата выписки (импорт) - Дата
// 		Ключ Организация - Организация - Справочник.Ссылки.Организация          
// 		Ключ КодВБанке - код в банке (если есть) 
//		Ключ РСчет - Расчетный счет
// 
// Возвращаемое значение:
//
//   имя файла по умолчанию
//
Функция СформироватьИмяФайла(Параметры) Экспорт 
	
	ИмяФайла = ПолучитьСохраненнуюНастройку("ИмяФайла", Параметры);
	Если Параметры.Режим = "Импорт" Тогда
		Если ИмяФайла = Неопределено Тогда
			Возврат "Export" + "." + НРег(Параметры.ТипФайла);  //здесь бы еще Параметры.ТипФайла не помешал бы для расширения!
		Иначе
			Возврат ИмяФайла;
		КонецЕсли;
	Иначе	
		ДатаСтр = ТекущаяДата();
		сФайл = Формат(ДатаСтр,"ДФ=yyMMdd") + ".dat"; // преобразуем в вид ГГММДД		
		Каталог = ПолучитьКаталог(ИмяФайла);
		Возврат Каталог + сФайл;
	КонецЕсли;
	
КонецФункции

// Возвращает сохраненный тип файла
//
// Параметры:
//
// 	Структура Параметры
//		Ключ Режим - Режим - Строка "Импорт"/"Экспорт"
//
// Возвращаемое значение:
//
//   Строка – текущий тип файла ("TXT"/"DBF"/"XML")
Функция ПолучитьТекущийТипФайла(Параметры) Экспорт
	
	ТипФайла = ПолучитьСохраненнуюНастройку("ТипФайла", Параметры);
	Если Параметры.Режим = "Импорт" Тогда
		Если ТипФайла = Неопределено Тогда
			Возврат "DBF";
		Иначе
			Возврат ТипФайла;
		КонецЕсли;		
	Иначе 
		 //Параметры.Режим = "Экспорт"
		Если ТипФайла = Неопределено Тогда
			Возврат "TXT";
		Иначе
		  Возврат ТипФайла;
		КонецЕсли;	
	КонецЕсли;	

КонецФункции

// Возвращает сохраненный кодировку файла
//
// Параметры:
//
// 	Структура Параметры
//		Ключ Режим - Режим - Строка "Импорт"/"Экспорт"
//
// Возвращаемое значение:
//
//   Строка – текущая кодировка файла ( "ANSI" / "OEM" )
Функция ПолучитьТекущуюКодировкуФайла(Параметры) Экспорт
	
	КодировкаФайла = ПолучитьСохраненнуюНастройку("КодировкаФайла", Параметры);
	
	Если Параметры.Режим = "Импорт" Тогда
		Если КодировкаФайла = Неопределено Тогда
			Возврат "ANSI"; // ANSI (Windows) или OEM (DOS)
		Иначе
			Возврат КодировкаФайла;
		КонецЕсли;	
	Иначе 
		// Параметры.Режим = "Экспорт"
		Если КодировкаФайла = Неопределено Тогда
			Возврат "ANSI"; // ANSI (Windows) или OEM (DOS)
		Иначе
			Возврат КодировкаФайла;
		КонецЕсли;	
	КонецЕсли;	

КонецФункции

// Обработка формата должна вернуть режимы и типы файлов, которая она поддерживает.
//
// Параметры: нет
//
//  Возвращаемое значение:
//
//	Возвращает структуру со следующими ключами:
//
//	Импорт (Истина/Ложь)
//	ИмпортTXT (Истина/Ложь)
//	ИмпортDBF (Истина/Ложь)
//	ИмпортXML (Истина/Ложь)
//	Экспорт (Истина/Ложь)
//	ЭкспортTXT (Истина/Ложь) 
//	ЭкспортDBF (Истина/Ложь)
//	ЭкспортXML (Истина/Ложь)
//
Функция ПолучитьПоддерживаемыеРежимы() Экспорт
	
	ИмпортЭкспорт = Новый Структура;
	ИмпортЭкспорт.Вставить("Импорт", Истина);
	ИмпортЭкспорт.Вставить("ИмпортTXT", Истина);
	ИмпортЭкспорт.Вставить("ИмпортDBF", Истина);
	ИмпортЭкспорт.Вставить("ИмпортXML", Ложь);
	
	ИмпортЭкспорт.Вставить("Экспорт", Истина);
	ИмпортЭкспорт.Вставить("ЭкспортTXT", Истина);
	ИмпортЭкспорт.Вставить("ЭкспортDBF", Ложь);
	ИмпортЭкспорт.Вставить("ЭкспортXML", Ложь);
	
	Возврат ИмпортЭкспорт;
	
КонецФункции

// Осуществляет импорт выписки из файла типа "TXT"
//
// Параметры:
//
//	Структура Параметры   
//
// 		Ключ ДатаДок - Дата выписки  – дата 
// 		Ключ Организация - Организация - СправочникСсылка.Организации
//		Ключ РСчет - Расчетный счет организации - строка
//		Ключ ИмяФайла - Имя файла  - строка  
//		Ключ ТипФайла - Тип файла  - строка ("TXT"/"DBF"/"XML")
//
//
// Возвращаемое значение:
//
//   Таблица значений тзВыписка
//		
//		тзВыписка - Таблица платежей – таблица значений тзВыписка с полями 
//			НомерПП – строка, номер платежного поручения 
//			Приход – число, сумма прихода
//			Расход – число, сумма расхода
//			ОКПО – строка, ЕДРПОУ контрагента
//			Контрагент – строка, наименование контрагента, как оно указано в выписке
//			МФО - строка, МФО банка контрагента
//			РСчет - строка, расчетный счет контрагента
//			Содержание - строка, содержание платежа
//
//		Если при импорте выписки произошли ошибки, возвращается Неопределенно.
//      
//      При проверке на дату платежа, если платежей с датой, которая отличается от переданной было > 0
//      необходимо выдать предупреждение
//
Функция ИмпортTXT(Параметры)
	ФайлTXT	= Новый ТекстовыйДокумент;
	ИмяФайла = Параметры.ИмяФайла;
	Попытка
		ФайлTXT.Прочитать(ИмяФайла, КодировкаТекста[Параметры.КодировкаФайла]); //Windows
	Исключение
		Возврат "Ошибка открытия файла " + ИмяФайла;
	КонецПопытки;		
	
	тзВыписка =  Новый ТаблицаЗначений;
	тзВыписка.Колонки.Добавить("НомерПП");
	тзВыписка.Колонки.Добавить("Приход");
	тзВыписка.Колонки.Добавить("Расход");
	тзВыписка.Колонки.Добавить("ОКПО");
	тзВыписка.Колонки.Добавить("Контрагент");
	тзВыписка.Колонки.Добавить("МФО");
	тзВыписка.Колонки.Добавить("РСчет");
	тзВыписка.Колонки.Добавить("Содержание");
	
	Для н = 1 По ФайлTXT.КоличествоСтрок() Цикл
		
		ТекСтрока = ФайлTXT.ПолучитьСтроку(н);
		
		Если СокрЛП(Сред(ТекСтрока, 10, 14)) = Параметры.РСчет Тогда // списание со счета
			НовСтрока = тзВыписка.Добавить();
			НовСтрока.МФО 			= СокрЛП(Сред(ТекСтрока, 24, 9));
			НовСтрока.РСчет			= СокрЛП(Сред(ТекСтрока, 33, 14));
			НовСтрока.Расход		= Цел(СокрЛП(Сред(ТекСтрока, 48, 16)))/100; // сумма указана в копейках
			НовСтрока.Приход		= 0;
			НовСтрока.Контрагент	= СокрЛП(Сред(ТекСтрока, 129, 38));
			НовСтрока.ОКПО			= СокрЛП(Сред(ТекСтрока, 406, 14)); // ??? возможно, ОКПО там нет
		ИначеЕсли СокрЛП(Сред(ТекСтрока, 33, 14)) = Параметры.РСчет Тогда // поступление на наш р/с
			НовСтрока = тзВыписка.Добавить();
			НовСтрока.МФО 			= СокрЛП(Сред(ТекСтрока, 1, 9));
			НовСтрока.РСчет			= СокрЛП(Сред(ТекСтрока, 10, 14));
			НовСтрока.Приход		= Цел(СокрЛП(Сред(ТекСтрока, 48, 16)))/100; // сумма указана в копейках
			НовСтрока.Расход		= 0;
			НовСтрока.Контрагент	= СокрЛП(Сред(ТекСтрока, 91, 38));
			НовСтрока.ОКПО			= СокрЛП(Сред(ТекСтрока, 392, 14));
		Иначе
			Продолжить;
		КонецЕсли;
		НовСтрока.Содержание 		= СокрЛП(Сред(ТекСтрока, 167, 160));
		//НовСтрока.НомерПП	 		= Цел(СокрЛП(Сред(ТекСтрока, 66, 10)));
		НовСтрока.НомерПП	 		= СокрЛП(Сред(ТекСтрока, 66, 10));
		
	КонецЦикла;	
	
	Если НЕ тзВыписка.Количество() И ФайлTXT.КоличествоСтрок() Тогда
		Возврат "В текущем файле нет платежей с данной датой выписки и расчетным счетом!";
	КонецЕсли;
	
	Возврат тзВыписка;	
КонецФункции	

// Осуществляет импорт выписки из файла типа "XML"
//
// Параметры:
//
//	Структура Параметры   
//
// 		Ключ ДатаДок - Дата выписки  – дата 
// 		Ключ Организация - Организация - СправочникСсылка.Организации
//		Ключ РСчет - Расчетный счет организации - строка
//		Ключ ИмяФайла - Имя файла  - строка  
//		Ключ ТипФайла - Тип файла  - строка ("TXT"/"DBF"/"XML")
//
//
// Возвращаемое значение:
//
//   Таблица значений тзВыписка
//		
//		тзВыписка - Таблица платежей – таблица значений тзВыписка с полями 
//			НомерПП – строка, номер платежного поручения 
//			Приход – число, сумма прихода
//			Расход – число, сумма расхода
//			ОКПО – строка, ЕДРПОУ контрагента
//			Контрагент – строка, наименование контрагента, как оно указано в выписке
//			МФО - строка, МФО банка контрагента
//			РСчет - строка, расчетный счет контрагента
//			Содержание - строка, содержание платежа
//
//		Если при импорте выписки произошли ошибки, возвращается Неопределенно.
//      
//      При проверке на дату платежа, если платежей с датой, которая отличается от переданной было > 0
//      необходимо выдать предупреждение
//
Функция ИмпортXML(Параметры)
	Возврат Неопределено;	
КонецФункции	

// Осуществляет импорт выписки из файла типа "DBF"
//
// Параметры:
//
//	Структура Параметры   
//
// 		Ключ ДатаДок - Дата выписки  – дата 
// 		Ключ Организация - Организация - СправочникСсылка.Организации
//		Ключ РСчет - Расчетный счет организации - строка
//		Ключ ИмяФайла - Имя файла  - строка  
//		Ключ ТипФайла - Тип файла  - строка ("TXT"/"DBF"/"XML")
//
//
// Возвращаемое значение:
//
//   Таблица значений тзВыписка
//		
//		тзВыписка - Таблица платежей – таблица значений тзВыписка с полями 
//			НомерПП – строка, номер платежного поручения 
//			Приход – число, сумма прихода
//			Расход – число, сумма расхода
//			ОКПО – строка, ЕДРПОУ контрагента
//			Контрагент – строка, наименование контрагента, как оно указано в выписке
//			МФО - строка, МФО банка контрагента
//			РСчет - строка, расчетный счет контрагента
//			Содержание - строка, содержание платежа
//
//		Если при импорте выписки произошли ошибки, возвращается Неопределенно.
//      
//      При проверке на дату платежа, если платежей с датой, которая отличается от переданной было > 0
//      необходимо выдать предупреждение
//
Функция ИмпортDBF(Параметры)
	
	ФайлDBF = Новый XBase;	
	ИмяФайла = Параметры.ИмяФайла;
	
	Попытка
		ФайлDBF.ОткрытьФайл(ИмяФайла,,Ложь); //Только для чтения
		ФайлDBF.Кодировка = КодировкаXBase[Параметры.КодировкаФайла];
	Исключение
		Возврат "Ошибка открытия файла " + ИмяФайла;
	КонецПопытки;	
		
	Если НЕ ФайлDBF.Открыта() Тогда
		Возврат "Ошибка открытия файла " + ИмяФайла;
	КонецЕсли;
	
	тзВыписка = Новый ТаблицаЗначений;
	тзВыписка.Колонки.Добавить("НомерПП");
	тзВыписка.Колонки.Добавить("Приход");
	тзВыписка.Колонки.Добавить("Расход");
	тзВыписка.Колонки.Добавить("ОКПО");
	тзВыписка.Колонки.Добавить("Контрагент");
	тзВыписка.Колонки.Добавить("МФО");
	тзВыписка.Колонки.Добавить("РСчет");
	тзВыписка.Колонки.Добавить("Содержание");
	
	ФайлDBF.Первая();
	Пока НЕ ФайлDBF.ВКонце() Цикл
		// проверка на дату платежа
		Если ФайлDBF.DAT_PR <> Параметры.ДатаДок Тогда
			ФайлDBF.Следующая();
			Продолжить;
		КонецЕсли;	
		
		// проверка на совпадение расчетного счета
		Если (СокрЛП(Формат(ФайлDBF.RR_DB, "ЧГ=0")) = Параметры.РСчет) или (СокрЛП(Формат(ФайлDBF.RR_K, "ЧГ=0")) = Параметры.РСчет) Тогда
			
			//поступление на счет
			Если СокрЛП(Формат(ФайлDBF.RR_DB, "ЧГ=0")) = Параметры.РСчет Тогда
					НовСтрока = тзВыписка.Добавить();
					НовСтрока.РСчет			= СокрЛП(Формат(ФайлDBF.RR_K,"ЧГ=0"));
					НовСтрока.МФО 			= СокрЛП(Формат(ФайлDBF.MFO_CR,"ЧГ=0"));
				    НовСтрока.ОКПО			= СокрЛП(Формат(ФайлDBF.OKPO_CR,"ЧГ=0"));
					НовСтрока.Контрагент	= СокрЛП(ФайлDBF.NAIM_K);
					НовСтрока.Приход		= 0;
					НовСтрока.Расход		= ФайлDBF.SUM;
					НовСтрока.Содержание 	= СтрЗаменить(ФайлDBF.PRIZN,Символы.ПС," ");

					//Сп.Установить("Банк",СокрЛП(ФайлБД.KLI_BAN_NA));
			ИначеЕсли СокрЛП(Формат(ФайлDBF.RR_K, "ЧГ=0")) = Параметры.РСчет Тогда
					НовСтрока = тзВыписка.Добавить();
					НовСтрока.РСчет			= СокрЛП(Формат(ФайлDBF.RR_DB,"ЧГ=0"));
					НовСтрока.МФО 			= СокрЛП(Формат(ФайлDBF.MFO_DB,"ЧГ=0"));
				    НовСтрока.ОКПО			= СокрЛП(Формат(ФайлDBF.OKPO_DB,"ЧГ=0"));
					НовСтрока.Контрагент	= СокрЛП(ФайлDBF.NAIM_D);
					НовСтрока.Приход		= ФайлDBF.SUM;
					НовСтрока.Расход		= 0;
					НовСтрока.Содержание 	= СтрЗаменить(ФайлDBF.PRIZN,Символы.ПС," ");
				//	//Сп.Установить("Банк",СокрЛП(ФайлБД.ACC_BAN_NA));
			КонецЕсли;
		Иначе	
			ФайлDBF.Следующая();
			Продолжить;
		КонецЕсли;
		
		ФайлDBF.Следующая();
	КонецЦикла;	
	
	Если НЕ тзВыписка.Количество() И ФайлDBF.КоличествоЗаписей() Тогда
		Возврат "В текущем файле нет платежей с данной датой выписки и расчетным счетом!";
	КонецЕсли;
	
	ФайлDBF.ЗакрытьФайл();
	
	Возврат тзВыписка;
	
КонецФункции	

// Осуществляет импорт выписки из файла
//
// Параметры:
//
//	Структура Параметры   
//
// 		Ключ ДатаДок - Дата выписки  – дата 
// 		Ключ Организация - Организация - СправочникСсылка.Организации
//		Ключ РСчет - Расчетный счет организации - строка
//		Ключ ИмяФайла - Имя файла  - строка  
//		Ключ ТипФайла - Тип файла  - строка ("TXT"/"DBF"/"XML")
//
//
// Возвращаемое значение:
//
//   Таблица значений тзВыписка
//		
//		тзВыписка - Таблица платежей – таблица значений тзВыписка с полями 
//			НомерПП – строка, номер платежного поручения 
//			Приход – число, сумма прихода
//			Расход – число, сумма расхода
//			ОКПО – строка, ЕДРПОУ контрагента
//			Контрагент – строка, наименование контрагента, как оно указано в выписке
//			МФО - строка, МФО банка контрагента
//			РСчет - строка, расчетный счет контрагента
//			Содержание - строка, содержание платежа
//
//		Если при импорте выписки произошли ошибки, возвращается Неопределенно.
//      
//      При проверке на дату платежа, если платежей с датой, которая отличается от переданной было > 0
//      необходимо выдать предупреждение
//
Функция ИмпортВыписки(Параметры) Экспорт
	Если Параметры.ТипФайла = "DBF" Тогда
		Возврат ИмпортDBF(Параметры);	
	ИначеЕсли Параметры.ТипФайла = "TXT" Тогда
		Возврат ИмпортTXT(Параметры);
	ИначеЕсли Параметры.ТипФайла = "XML" Тогда
		Возврат ИмпортXML(Параметры);
	КонецЕсли;	
КонецФункции

Функция ВыровнятьВправо(Стр, Длина, Символ = " ")
	СтрокаПробелов 	= "                                                                ";
	СтрокаНулей 	= "0000000000";
	Если Символ = " " Тогда
		Строка = СтрокаПробелов;
	Иначе
		Строка = СтрокаНулей;
	КонецЕсли;	
	Стр = СокрЛП(Строка(Стр));
	ДлинаСтр = СтрДлина(Стр);
	Стр = Лев(Строка,Длина - ДлинаСтр) + Стр;
	
	Возврат Лев(Стр, Длина); // обрезаем строку, если ее длинна больше допустимой
КонецФункции

// Осуществляет экспорт платежных поручений в файл типа "TXT"
//
// Параметры:
//
//	Структура Параметры   
//                 
//		Ключ НашМФО - МФО организации - строка
//		Ключ НашСчет - 	Расчетный счет организации - строка
//      Ключ НашОКПО - 	ЕДРПОУ организации  - строка
//		Ключ ИмяФайла - Имя файла  - строка  
//		Ключ ТипФайла - Тип файла  - строка ("TXT"/"DBF"/"XML")
//		Ключ тзПлатежныеПоручения - Таблица значений с полями:
//			Номер платежного поручения (НомерПП) - строка
//			Дата платежного поручения (ДатаПП) - строка
//			Контрагент (Контрагент) - строка
//			МФО расчетного счета контрагента (МФО) - строка
//			Расчетный счет контрагента (Счет) - строка
//			ЕДРПОУ контрагента (ОКПО) - строка
//			Сумма платежного поручения (Сумма) – число 
//			Содержание платежа (Содержание) – строка 
//			!!!(Банк) - строка
//
// Возвращаемое значение:
//
//   Булево - Истина - успешный экспорт
//
Функция ЭкспортTXT(Параметры)
	Текст = Новый ТекстовыйДокумент;
	СтрокаПробелов 	= "                                                                                                                                                                                                ";
	
	Для Каждого Строка из Параметры.тзПлатежныеПоручения Цикл
		
		ТекСтрока = ВыровнятьВправо(Параметры.НашМФО, 9);
		ТекСтрока = ТекСтрока + ВыровнятьВправо(Параметры.НашСчет, 14);
		ТекСтрока = ТекСтрока + ВыровнятьВправо(Строка.МФО, 9);
		ТекСтрока = ТекСтрока + ВыровнятьВправо(Строка.Счет, 14);
		ТекСтрока = ТекСтрока + "1"; // тип операции
		ТекСтрока = ТекСтрока + ВыровнятьВправо(Формат(Строка.Сумма*100, "ЧН=0;ЧГ=0"),16);
		ТекСтрока = ТекСтрока + ВыровнятьВправо("1", 2); // Вид платежа - 1 - платежка, 6 - мемориальный ордер
		ТекСтрока = ТекСтрока + Лев(Строка.НомерПП + СтрокаПробелов, 10);
		ТекСтрока = ТекСтрока + "980";
		ТекСтрока = ТекСтрока + Формат(Строка.ДатаПП,"ДФ=yyMMdd");
		ТекСтрока = ТекСтрока + Лев("" + СтрокаПробелов, 6); // Дата получения документа в банке
		ТекСтрока = ТекСтрока + Лев(Параметры.НашаФирма + СтрокаПробелов, 38);
		ТекСтрока = ТекСтрока + Лев(Строка.Контрагент + СтрокаПробелов, 38);
		ТекСтрока = ТекСтрока + Лев(Строка.Содержание + СтрокаПробелов, 160);
		
		// ТекСтрока = ТекСтрока + Формат("#o"+Сп.Получить("ОКПО"), "с60"); //Дополнительные реквизиты
		
		// теперь просто пустота, ОКПО скорей всего ушло в поле 
		// № 15, "Дополнительные реквизиты". В этом поле указывается код страны 
		// плательшика/получателя если плательшик/получатель 
		// является не резидентом в следующем формате: 
		// #nП764О018#, ("П" и "О" русские заглавные символы) 
		// где "764" - код страны плательщика, "018" - код страны получателя. 
		ТекСтрока = ТекСтрока + Лев("" + СтрокаПробелов, 60); //Дополнительные реквизиты                        
		
		// № 16, "Код назначения платежа". 
		// Следует указывать код "070" для платежей в бюджет 
		// и оставлять поле пустым для всех прочих платежей		
		ТекСтрока = ТекСтрока + Лев(Строка.НомерПП + "", 5); //Код назначения платежа и еще что-то (поля №16-17)
		ТекСтрока = ТекСтрока + Лев(Параметры.НашОКПО + СтрокаПробелов, 14);
		// ТекСтрока = ТекСтрока + Формат("", "с187");
		ТекСтрока = ТекСтрока + Лев(Строка.ОКПО + СтрокаПробелов, 14);
		ТекСтрока = ТекСтрока + Лев("" + СтрокаПробелов, 173);
		
		Текст.ДобавитьСтроку(ТекСтрока);			
	
	КонецЦикла;          
	
	ИмяФайла = Параметры.ИмяФайла;	
	Попытка
		Текст.Записать(ИмяФайла, КодировкаТекста[Параметры.КодировкаФайла]);
	Исключение
		Возврат "Ошибка записи файла " + ИмяФайла;
	КонецПопытки;		
	
	Возврат Истина;
КонецФункции

// Осуществляет экспорт платежных поручений в файл типа "XML"
//
// Параметры:
//
//	Структура Параметры   
//                 
//		Ключ НашМФО - МФО организации - строка
//		Ключ НашСчет - 	Расчетный счет организации - строка
//      Ключ НашОКПО - 	ЕДРПОУ организации  - строка
//		Ключ ИмяФайла - Имя файла  - строка  
//		Ключ ТипФайла - Тип файла  - строка ("TXT"/"DBF"/"XML")
//		Ключ тзПлатежныеПоручения - Таблица значений с полями:
//			Номер платежного поручения (НомерПП) - строка
//			Дата платежного поручения (ДатаПП) - строка
//			Контрагент (Контрагент) - строка
//			МФО расчетного счета контрагента (МФО) - строка
//			Расчетный счет контрагента (Счет) - строка
//			ЕДРПОУ контрагента (ОКПО) - строка
//			Сумма платежного поручения (Сумма) – число 
//			Содержание платежа (Содержание) – строка 
//			!!!(Банк) - строка
//
// Возвращаемое значение:
//
//   Булево - Истина - успешный экспорт
//
Функция ЭкспортXML(Параметры)
	Возврат Ложь;
КонецФункции

// Осуществляет экспорт платежных поручений в файл типа "DBF"
//
// Параметры:
//
//	Структура Параметры   
//                 
//		Ключ НашМФО - МФО организации - строка
//		Ключ НашСчет - 	Расчетный счет организации - строка
//      Ключ НашОКПО - 	ЕДРПОУ организации  - строка
//		Ключ ИмяФайла - Имя файла  - строка  
//		Ключ ТипФайла - Тип файла  - строка ("TXT"/"DBF"/"XML")
//		Ключ тзПлатежныеПоручения - Таблица значений с полями:
//			Номер платежного поручения (НомерПП) - строка
//			Дата платежного поручения (ДатаПП) - строка
//			Контрагент (Контрагент) - строка
//			МФО расчетного счета контрагента (МФО) - строка
//			Расчетный счет контрагента (Счет) - строка
//			ЕДРПОУ контрагента (ОКПО) - строка
//			Сумма платежного поручения (Сумма) – число 
//			Содержание платежа (Содержание) – строка 
//			!!!(Банк) - строка
//
// Возвращаемое значение:
//
//   Булево - Истина - успешный экспорт
//
Функция ЭкспортDBF(Параметры)
	Возврат Ложь;
КонецФункции

// Осуществляет экспорт платежных поручений в файл
//
// Параметры:
//
//	Структура Параметры   
//                 
//		Ключ НашМФО - МФО организации - строка
//		Ключ НашСчет - 	Расчетный счет организации - строка
//      Ключ НашОКПО - 	ЕДРПОУ организации  - строка
//		Ключ ИмяФайла - Имя файла  - строка  
//		Ключ ТипФайла - Тип файла  - строка ("TXT"/"DBF"/"XML")
//		Ключ тзПлатежныеПоручения - Таблица значений с полями:
//			Номер платежного поручения (НомерПП) - строка
//			Дата платежного поручения (ДатаПП) - строка
//			Контрагент (Контрагент) - строка
//			МФО расчетного счета контрагента (МФО) - строка
//			Расчетный счет контрагента (Счет) - строка
//			ЕДРПОУ контрагента (ОКПО) - строка
//			Сумма платежного поручения (Сумма) – число 
//			Содержание платежа (Содержание) – строка 
//
// Возвращаемое значение:
//
//   Булево - Истина - успешный экспорт
//
Функция ЭкспортПлатежныхПоручений(Параметры) Экспорт
	Если Параметры.ТипФайла = "DBF" Тогда
		Возврат ЭкспортDBF(Параметры);	
	ИначеЕсли Параметры.ТипФайла = "TXT" Тогда
		Возврат ЭкспортTXT(Параметры);
	ИначеЕсли Параметры.ТипФайла = "XML" Тогда
		Возврат ЭкспортXML(Параметры);
	КонецЕсли;	
		
КонецФункции

// Надо ли перекодировать текстовые поля (Контрагент, Содержание) после импорта. 
// Под перекодировкой понимается замена украинских символов
//
// Параметры: нет
//
//
// Возвращаемое значение:
//
//   Булево    – Истина - надо перекодировать/Ложь - не надо перекодировать
//
Функция ПерекодировкаИмпорта() Экспорт
	Возврат Ложь;
КонецФункции

// Надо ли перекодировать текстовые поля (Контрагент, Содержание) перед экспортом. 
// Под перекодировкой понимается замена украинских символов
//
// Параметры: нет
//
// Возвращаемое значение:
//
//   Булево    – Истина - надо перекодировать/Ложь - не надо перекодировать
Функция ПерекодировкаЭкспорта() Экспорт
	Возврат Ложь;
КонецФункции

// Формирует имя файла по умолчанию
//
// Параметры:
//
// 	Структура Параметры
//		Ключ Режим - Режим - Строка "Импорт"/"Экспорт"
// 		Ключ Дата -  Дата выписки (импорт) - Дата
// 		Ключ Организация - Организация - Справочник.Ссылки.Организация          
// 		Ключ КодВБанке - код в банке (если есть) 
//		Ключ РСчет - Расчетный счет
// 
// Возвращаемое значение:
//
//   имя файла по умолчанию
//
Функция СформироватьФильтрВыбораФайла(Параметры) Экспорт 
	Если Параметры.ТипФайла = "TXT" Тогда
		Возврат НСтр("ru = 'Текстовые файлы (*.*)|*.*'");
	ИначеЕсли Параметры.ТипФайла = "DBF" Тогда
		Возврат НСтр("ru='Файлы dBase (*.dbf)|*.dbf'");
	ИначеЕсли Параметры.ТипФайла = "XML" Тогда
		Возврат НСтр("ru='XML файлы (*.xml)|*.xml'");
	Иначе	
		Возврат НСтр("ru='Все файлы (*.*)|*.*'");
	КонецЕсли;	
КонецФункции

Функция ПолучитьСохраненнуюНастройку(ИмяНастройки, Параметры) Экспорт
	
	Если ИмяНастройки = "КодировкаФайла" Тогда
		
		ИмяКлючаНастроек = ИмяФормата + Параметры.Организация + Параметры.Режим;
		Настройки = ХранилищеСистемныхНастроек.Загрузить("ВнешняяОбработка.КлиентБанк2/ОбработкаФормата", "КлиентБанк2" + ИмяКлючаНастроек);
	
		Если Настройки <> Неопределено Тогда
			Возврат Настройки.Получить(ИмяНастройки + ИмяКлючаНастроек);
		КонецЕсли;	
		
	ИначеЕсли ИмяНастройки = "ТипФайла" Тогда
		
		ИмяКлючаНастроек = ИмяФормата + Параметры.Организация + Параметры.Режим;
		Настройки = ХранилищеСистемныхНастроек.Загрузить("ВнешняяОбработка.КлиентБанк2/ОбработкаФормата", "КлиентБанк2" + ИмяКлючаНастроек);
	
		Если Настройки <> Неопределено Тогда
			Возврат Настройки.Получить(ИмяНастройки + ИмяКлючаНастроек);
		КонецЕсли;	
		
	ИначеЕсли ИмяНастройки = "КодВБанке" Тогда
		
		ИмяКлючаНастроек = ИмяФормата + Параметры.Организация + Параметры.Режим;
		Настройки = ХранилищеСистемныхНастроек.Загрузить("ВнешняяОбработка.КлиентБанк2/ОбработкаФормата", "КлиентБанк2" + ИмяКлючаНастроек);
	
		Если Настройки <> Неопределено Тогда
			Возврат Настройки.Получить(ИмяНастройки + ИмяКлючаНастроек);
		КонецЕсли;	
		
	ИначеЕсли ИмяНастройки = "ИмяФайла" Тогда
		
		ИмяКлючаНастроек = ИмяФормата + Параметры.Организация + Параметры.Режим + Параметры.ТипФайла;
		Настройки = ХранилищеСистемныхНастроек.Загрузить("ВнешняяОбработка.КлиентБанк2/ОбработкаФормата", "КлиентБанк2" + ИмяКлючаНастроек);
	
		Если Настройки <> Неопределено Тогда
			Возврат Настройки.Получить(ИмяНастройки + ИмяКлючаНастроек);
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат "";
КонецФункции

// Сохраняет настройки 
Процедура СохранитьНастройки(Параметры) Экспорт
	
	Настройки = Новый Соответствие;
	ИмяКлючаНастроек = ИмяФормата + Параметры.Организация + Параметры.Режим;
	Настройки.Вставить("ТипФайла" 		+ ИмяКлючаНастроек, Параметры.ТекущийТипФайла);
	Настройки.Вставить("КодировкаФайла" + ИмяКлючаНастроек, Параметры.КодировкаФайла);
	Настройки.Вставить("КодВБанке" 		+ ИмяКлючаНастроек, Параметры.КодВБанке);
	ХранилищеСистемныхНастроек.Сохранить("ВнешняяОбработка.КлиентБанк2/ОбработкаФормата", "КлиентБанк2" + ИмяКлючаНастроек, Настройки);
	
	Настройки = Новый Соответствие;
	ИмяКлючаНастроек = ИмяФормата + Параметры.Организация + Параметры.Режим + Параметры.ТекущийТипФайла;
	Настройки.Вставить("ИмяФайла" 		+ ИмяКлючаНастроек, Параметры.ТекущееИмяФайла);
	ХранилищеСистемныхНастроек.Сохранить("ВнешняяОбработка.КлиентБанк2/ОбработкаФормата", "КлиентБанк2" + ИмяКлючаНастроек, Настройки);
	
КонецПроцедуры // СохранитьНастройкиФормы()

ИмяФормата = "CSBankOnLine";
ИмяФормата = Метаданные().Имя;
