////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	Если Приход > 0 Тогда	// Приход
		типДокСделки = ПолучитьИмяОбъектаКонфигурации("ТипыДокументовСделкиПриход");
	Иначе	
		типДокСделки = ПолучитьИмяОбъектаКонфигурации("ТипыДокументовСделкиРасход");
	КонецЕсли;	

	Массив = Новый Массив;
	Для Каждого ТипСделки Из типДокСделки Цикл
		Массив.Добавить(Тип("ДокументСсылка."+ ТипСделки.Значение));
	КонецЦикла;	
	
	ОписаниеТипаСделки = Новый ОписаниеТипов(Массив); 
	
	ЭлементыФормы.Сделки.Колонки.Сделка.ЭлементУправления.ОграничениеТипа = ОписаниеТипаСделки;
	
КонецПроцедуры

Процедура ДействияФормыУстановитьФлаги(Кнопка)
	УстановитьФлажок(Истина,Сделки);
КонецПроцедуры

Процедура ДействияФормыСнятьФлаги(Кнопка)
	УстановитьФлажок(Ложь,Сделки);
КонецПроцедуры

Процедура ДействияФормыИнвертироватьФлаги(Кнопка)
	УстановитьФлажок(Неопределено,Сделки);
КонецПроцедуры

Процедура УстановитьСуммуВсегоРаспределено()
	ВсегоРаспределено = 0;
	Для Каждого СтрокаСделки Из Сделки Цикл
		Если СтрокаСделки.Флаг Тогда
			ВсегоРаспределено = ВсегоРаспределено + СтрокаСделки.СуммаКБ;
		КонецЕсли;
	КонецЦикла;
	
	Если ВсегоРаспределено <> СуммаКРаспределению Тогда
		ЭлементыФормы.ВсегоРаспределено.ЦветТекстаПоля = WebЦвета.Красный;	
	Иначе	
		ЭлементыФормы.ВсегоРаспределено.ЦветТекстаПоля = WebЦвета.ЗеленыйЛес;	
	КонецЕсли;	
КонецПроцедуры	
	
Процедура ОбновлениеОтображения()
	УстановитьСуммуВсегоРаспределено();
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если ВсегоРаспределено <> СуммаКРаспределению Тогда
		ТекстВопроса = Нстр("ru = 'Общая сумма к распределению не совпадает с итоговой суммой распределения всех сделок!
        |Продолжить?'; uk = 'Загальна сума до розподілу не збігається з підсумковою сумою розподілу всіх угод!
        |Продожити?'"); 
		
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;	
	
	Закрыть(Истина);
КонецПроцедуры

Процедура СделкиСделкаПриИзменении(Элемент)
	ТС = ЭлементыФормы.Сделки.ТекущаяСтрока;
	Если ТС = Неопределено ИЛИ ТС.Сделка = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекСуммаДокумента = ТС.Сделка.СуммаДокумента;
	Если ТС.СуммаДокументаСделки <> ТекСуммаДокумента Тогда
		ТС.СуммаДокументаСделки = ТекСуммаДокумента;
		ТС.СуммаКБ 				= ТекСуммаДокумента;
	КонецЕсли;	
КонецПроцедуры

Процедура СделкиСделкаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Если Элемент.Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбораДокумента = Документы[Элемент.Значение.Метаданные().Имя].ПолучитьФормуВыбора(,Элемент,);
	ФормаВыбораДокумента.Отбор.Организация.Значение              = Организация;
	ФормаВыбораДокумента.Отбор.Организация.Использование         = Истина;
	
	ФормаВыбораДокумента.Отбор.Контрагент.Значение      = Контрагент;
	ФормаВыбораДокумента.Отбор.Контрагент.Использование = Истина;
	
	ФормаВыбораДокумента.НачальноеЗначениеВыбора = Элемент.Значение;
	ФормаВыбораДокумента.РежимВыбора = Истина;
	ФормаВыбораДокумента.Открыть();
	
КонецПроцедуры

Процедура СделкиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущаяСтрока.Флаг = Истина;
	КонецЕсли;	
КонецПроцедуры





