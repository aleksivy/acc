////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура УстановитьВидимостьОбособленногоПодразделения ()
	
	УчетнаяПолитикаНеЗадана = Ложь;
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(ДатаКон, УчетнаяПолитикаНеЗадана, Организация);
	
	БУ = ПланыСчетов.Хозрасчетный.КассаВНациональнойВалюте;
	ЕстьАналитикаПоОбособленнымПодразделениям = ?(БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбособленныеПодразделенияБезОбразованияЮрЛица, "ВидСубконто") = Неопределено, Ложь, Истина);
	
	УчетДенежныхСредствПоОбособленнымПодразделениям = Ложь;
	Если НЕ УчетнаяПолитикаНеЗадана Тогда	
		УчетДенежныхСредствПоОбособленнымПодразделениям = ЕстьАналитикаПоОбособленнымПодразделениям И УчетнаяПолитика.ВестиУчетДенежныхСредствПоОбособленнымПодразделениям;
	КонецЕсли;
	
	ЭлементыФормы.ПоОбособленномуПодразделению.Видимость = УчетДенежныхСредствПоОбособленнымПодразделениям;
	ЭлементыФормы.НадписьОбособленноеПодразделениеОрганизации.Видимость = УчетДенежныхСредствПоОбособленнымПодразделениям;
	ЭлементыФормы.ОбособленноеПодразделениеОрганизации.Видимость = УчетДенежныхСредствПоОбособленнымПодразделениям;
	
	Если УчетДенежныхСредствПоОбособленнымПодразделениям Тогда
		ЭлементыФормы.ПанельОбособленноеПодразделение.Свертка = РежимСверткиЭлементаУправления.Нет;
		
		ЭлементыФормы.НадписьОбособленноеПодразделениеОрганизации.Доступность = ПоОбособленномуПодразделению; 
		ЭлементыФормы.ОбособленноеПодразделениеОрганизации.Доступность = ПоОбособленномуПодразделению;
	Иначе
		ЭлементыФормы.ПанельОбособленноеПодразделение.Свертка = РежимСверткиЭлементаУправления.Верх;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВидимость()
	
	Если ВидОтчета = "Кассовая книга" Тогда
		ЭлементыФормы.Обычный.Видимость     = Истина;
		ЭлементыФормы.КонецМесяца.Видимость = Истина;
		ЭлементыФормы.КонецГода.Видимость   = Истина;
		ЭлементыФормы.ПанельНастройкаНумерацииСтраниц.Видимость   = Истина;
	Иначе
	    ЭлементыФормы.Обычный.Видимость     = Ложь;
		ЭлементыФормы.КонецМесяца.Видимость = Ложь;
		ЭлементыФормы.КонецГода.Видимость   = Ложь;
		ЭлементыФормы.ПанельНастройкаНумерацииСтраниц.Видимость   = Ложь;
	КонецЕсли;
	
	Если ВидОтчета = "Книга учета принятых и выданных денег" Тогда
		ЭлементыФормы.ВыводитьТитульныйЛист.Видимость = ложь;	
	Иначе	
		ЭлементыФормы.ВыводитьТитульныйЛист.Видимость = Истина;	
	КонецЕсли;
	
КонецПроцедуры	

Процедура ПолучитьНачальныйНомерЛиста()

	Если НЕ ВидОтчета = "Кассовая книга" Тогда
		Возврат;
	КонецЕсли;
	
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Организация);

	НесколькоЛистовВДень = Истина;
	НачальныйНомерЛиста = 1;
		
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Организация);
	НастройкиПечатиКассовойКниги = РегистрыСведений.НастройкиПечатиКассовойКниги.СрезПоследних(НачалоГода(ДатаНач), Отбор);
	Если НастройкиПечатиКассовойКниги.Количество() > 0 Тогда 
		НесколькоЛистовВДень = НастройкиПечатиКассовойКниги[0].НесколькоЛистовВДень;
		НачальныйНомерЛиста  = НастройкиПечатиКассовойКниги[0].НомерПервогоЛиста;
	КонецЕсли;
	
	НадписьНачальныйНомерЛиста = НСтр("ru='Номер первого листа - ';uk='Номер першого аркуша - '") + Строка(НачальныйНомерЛиста);
	НадписьНачальныйНомерЛиста = НадписьНачальныйНомерЛиста + ?(НесколькоЛистовВДень, НСтр("ru=', несколько листов в день';uk=', декілька аркушів у день'") , НСтр("ru=', один лист на день';uk=', один аркуш на день'"))
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ

// Процедура-обработчик события нажатия кнопки "Сформировать"
Процедура ДействияФормыСформировать(Кнопка)
	
	ПолучитьНесколькоЛистовВДень();
	СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ВидОтчета);
	
КонецПроцедуры


// Процедура - обработчик события "При открытии" формы отчета.
//
Процедура ПриОткрытии()

	БухгалтерскиеОтчеты.ПоддержкаОднофирменностиДляБухОтчетов(ЭтаФорма, Организация, глЗначениеПеременной("УчетПоВсемОрганизациям"), Истина);
	ЗаполнениеДокументов.ПриИзмененииЗначенияОрганизации(ЭтотОбъект,глЗначениеПеременной("глТекущийПользователь"));
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидОтчета) Тогда
		ВидОтчета = "Кассовая книга";
		ЭлементыФормы.Обычный.Значение = "Обычный";
	КонецЕсли;

	ЭлементыФормы.Организация.СписокВыбора = ПроцедурыУправленияПерсоналом.ПолучитьСписокГоловныхОрганизаций();
	ДатаНач=НачалоДня(РабочаяДата);
	ДатаКон=КонецДня(РабочаяДата);
	
	УстановитьВидимостьОбособленногоПодразделения();
	
	ПолучитьНачальныйНомерЛиста();
	
	УстановитьВидимость();
	
КонецПроцедуры // ПриОткрытии()

Процедура КнопкаНастройкаПериодаНажатие(Элемент)
	
	Если НП.Редактировать() Тогда
		
		ДатаНач = НП.ПолучитьДатуНачала();
		ДатаКон = НП.ПолучитьДатуОкончания();
		
	КонецЕсли;
	
	ПолучитьНачальныйНомерЛиста();
	
КонецПроцедуры

Процедура ВидОтчетаПриИзменении(Элемент)
	
	Если ВидОтчета = "Журнал регистрации приходных ордеров" Тогда
		ВидОтчетаПредставление = НСтр("ru='Журнал регистрации приходных ордеров';uk='Журнал реєстрації прибуткових ордерів'");
	ИначеЕсли ВидОтчета = "Журнал регистрации расходных ордеров" Тогда
		ВидОтчетаПредставление = НСтр("ru='Журнал регистрации расходных ордеров';uk='Журнал реєстрації видаткових ордерів'");
	ИначеЕсли ВидОтчета = "Журнал регистрации приходных и расходных ордеров" Тогда
		ВидОтчетаПредставление = НСтр("ru='Журнал регистрации приходных и расходных ордеров';uk='Журнал реєстрації прибуткових і видаткових ордерів'");
	ИначеЕсли ВидОтчета = "Книга учета принятых и выданных денег" Тогда
		ВидОтчетаПредставление = НСтр("ru='Книга учета принятых и выданных денег';uk='Книга обліку прийнятих і виданих грошей'");
	ИначеЕсли ВидОтчета = "Кассовая книга" Тогда
		ВидОтчетаПредставление = НСтр("ru='Кассовая книга';uk='Касова книга'");
	КонецЕсли;	
	
	ЭтаФорма.Заголовок = ВидОтчетаПредставление + НСтр("ru=' за период';uk=' за період'");
	ЭлементыФормы.ДокументРезультат.Очистить();
	УстановитьВидимость();
	Если НЕ Организация.Пустая() И НЕ Валюта.Пустая() Тогда
		СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ВидОтчета);
	КонецЕсли;	
КонецПроцедуры

Процедура ОбычныйПриИзменении(Элемент)
	
	ВидЛистаКассовойКниги = Элемент.Значение;
	
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнениеДокументов.ПриИзмененииЗначенияОрганизации(ЭтотОбъект,глЗначениеПеременной("глТекущийПользователь"));
	
	УстановитьВидимостьОбособленногоПодразделения();
	
	ПолучитьНачальныйНомерЛиста();
	
КонецПроцедуры

Процедура ПоОбособленномуПодразделениюПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ОбособленноеПодразделениеОрганизации) Тогда
		ОбособленноеПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),"ОсновноеОбособленноеПодразделениеОрганизации");
	КонецЕсли;
	Если НЕ ПоОбособленномуПодразделению Тогда
		ОбособленноеПодразделениеОрганизации = Неопределено;
	КонецЕсли;
	
	УстановитьВидимостьОбособленногоПодразделения();
	
КонецПроцедуры

Процедура ДатаНачПриИзменении(Элемент)
	
	ПолучитьНесколькоЛистовВДень()
	
КонецПроцедуры

Процедура ПоследниеНомераЛистовКассовойКниги(Кнопка)
	
	ФормаНастройка = РегистрыСведений.ПоследниеНомераЛистовКассовойКниги.ПолучитьФормуСписка();
	ФормаНастройка.Отбор.СтруктурнаяЕдиница.Значение = Организация;
	ФормаНастройка.Отбор.СтруктурнаяЕдиница.Использование = Истина;
	ФормаНастройка.Отбор.Валюта.Значение = Валюта;
	ФормаНастройка.Отбор.Валюта.Использование = Истина;
	ФормаНастройка.Открыть();
	
КонецПроцедуры

Процедура ВыводитьТитульныйЛистПриИзменении(Элемент)
	
	СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ВидОтчета);

КонецПроцедуры

Процедура НадписьНастроитьНумерациюСтраницКассовойКнигиНажатие(Элемент)
	
	ФормаНастройка = РегистрыСведений.НастройкиПечатиКассовойКниги.ПолучитьФормуСписка(, ЭтаФорма);
	ФормаНастройка.Отбор.Организация.Значение = Организация;
	ФормаНастройка.Отбор.Организация.Использование = Истина;
	ФормаНастройка.ОткрытьМодально();
	
	ПолучитьНесколькоЛистовВДень()

КонецПроцедуры

Процедура ПолучитьНесколькоЛистовВДень()
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Организация);
	НастройкиПечатиКассовойКниги = РегистрыСведений.НастройкиПечатиКассовойКниги.СрезПоследних(НачалоГода(ДатаНач), Отбор);
	Если НастройкиПечатиКассовойКниги.Количество() > 0 Тогда 
		НесколькоЛистовВДень = НастройкиПечатиКассовойКниги[0].НесколькоЛистовВДень;
		НачальныйНомерЛиста  = НастройкиПечатиКассовойКниги[0].НомерПервогоЛиста;
	КонецЕсли;
	
	НадписьНачальныйНомерЛиста = НСтр("ru='Номер первого листа - ';uk='Номер першого аркуша - '") + Строка(НачальныйНомерЛиста);
	НадписьНачальныйНомерЛиста = НадписьНачальныйНомерЛиста + ?(НесколькоЛистовВДень, НСтр("ru=', несколько листов в день';uk=', декілька аркушів у день'") , НСтр("ru=', один лист на день';uk=', один аркуш на день'"))
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ДИАЛОГА

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

