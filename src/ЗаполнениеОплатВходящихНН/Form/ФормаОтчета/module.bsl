
// ЗаполнитьОплаты4
//
Процедура ЗаполнитьОплаты4()
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("ДатаВПериоде", ДатаВПериоде);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	ДокТЧ.Ссылка
	|ПОМЕСТИТЬ ОплаченныеНН
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Оплаты КАК ДокТЧ
	|ГДЕ
	|	ДокТЧ.Ссылка.Проведен
	|	И ДокТЧ.Ссылка.Дата >= НАЧАЛОПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И ДокТЧ.Ссылка.Дата <= КОНЕЦПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И ДокТЧ.Ссылка.ДоговорКонтрагента.ВедениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом)
	|	И ТИПЗНАЧЕНИЯ(ДокТЧ.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокТЧ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Док.Ссылка,
	|	Док.Контрагент,
	|	Док.ДоговорКонтрагента,
	|	Док.ДокументОснование КАК ПриходнаяНакладная,
	|	СУММА(Док.СуммаДокумента) КАК СуммаПоНН
	|ПОМЕСТИТЬ ВходящиеНН
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента КАК Док
	|ГДЕ
	|	Док.Проведен
	|	И Док.Дата >= НАЧАЛОПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И Док.Дата <= КОНЕЦПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И Док.ДоговорКонтрагента.ВедениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом)
	|	И ТИПЗНАЧЕНИЯ(Док.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|	И НЕ Док.Ссылка В
	|				(ВЫБРАТЬ
	|					ОплаченныеНН.Ссылка
	|				ИЗ
	|					ОплаченныеНН КАК ОплаченныеНН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Док.Ссылка,
	|	Док.Контрагент,
	|	Док.ДоговорКонтрагента,
	|	Док.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Рег.Регистратор КАК Платеж,
	|	Рег.СубконтоДт1 КАК Контрагент,
	|	Рег.СубконтоДт2 КАК Договор,
	|	Рег.СубконтоДт3 КАК ПриходнаяНакладная,
	|	СУММА(Рег.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ПлатежиПоПриходам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			,
	|			Активность
	|				И СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСОтечественнымиПоставщиками)
	|				И СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТекущиеСчетаВНациональнойВалюте)
	|				И СубконтоДт2 В
	|					(ВЫБРАТЬ
	|						ВходящиеНН.ДоговорКонтрагента
	|					ИЗ
	|						ВходящиеНН КАК ВходящиеНН)
	|				И ТИПЗНАЧЕНИЯ(СубконтоДт3) = ТИП(Документ.ПоступлениеТоваровУслуг),
	|			,
	|			) КАК Рег
	|
	|СГРУППИРОВАТЬ ПО
	|	Рег.Регистратор,
	|	Рег.СубконтоДт1,
	|	Рег.СубконтоДт2,
	|	Рег.СубконтоДт3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВходящиеНН.Ссылка,
	|	ВходящиеНН.Контрагент,
	|	ВходящиеНН.ДоговорКонтрагента,
	|	ВходящиеНН.ПриходнаяНакладная,
	|	ВходящиеНН.СуммаПоНН,
	|	ПлатежиПоПриходам.Платеж,
	|	ПлатежиПоПриходам.Сумма
	|ПОМЕСТИТЬ Врем
	|ИЗ
	|	ВходящиеНН КАК ВходящиеНН
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлатежиПоПриходам КАК ПлатежиПоПриходам
	|		ПО ВходящиеНН.Контрагент = ПлатежиПоПриходам.Контрагент
	|			И ВходящиеНН.ДоговорКонтрагента = ПлатежиПоПриходам.Договор
	|			И ВходящиеНН.ПриходнаяНакладная = ПлатежиПоПриходам.ПриходнаяНакладная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем.Ссылка,
	|	Врем.Контрагент,
	|	Врем.ДоговорКонтрагента,
	|	Врем.ПриходнаяНакладная,
	|	Врем.СуммаПоНН
	|ПОМЕСТИТЬ Свод
	|ИЗ
	|	Врем КАК Врем
	|
	|СГРУППИРОВАТЬ ПО
	|	Врем.Ссылка,
	|	Врем.Контрагент,
	|	Врем.ДоговорКонтрагента,
	|	Врем.ПриходнаяНакладная,
	|	Врем.СуммаПоНН
	|
	|ИМЕЮЩИЕ
	|	Врем.СуммаПоНН = СУММА(Врем.Сумма)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем.Ссылка КАК НалоговаяНакладная,
	|	Врем.Платеж КАК ПлатежноеПоручение,
	|	Врем.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Врем.Сумма КАК Сумма
	|ИЗ
	|	Врем КАК Врем
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Свод КАК Свод
	|		ПО Врем.Ссылка = Свод.Ссылка
	|			И Врем.Контрагент = Свод.Контрагент
	|			И Врем.ДоговорКонтрагента = Свод.ДоговорКонтрагента
	|			И Врем.ПриходнаяНакладная = Свод.ПриходнаяНакладная
	|			И Врем.СуммаПоНН = Свод.СуммаПоНН
	|
	|УПОРЯДОЧИТЬ ПО
	|	Врем.Ссылка,
	|	ПлатежноеПоручение,
	|	ДоговорКонтрагента
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	НалоговаяНакладная
	|";
	лВыборкаНН = лЗапрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "НалоговаяНакладная");
	Пока лВыборкаНН.Следующий() Цикл
		ОбработкаПрерыванияПользователя();
		Состояние( "04. Обрабатывается НН № " + лВыборкаНН.НалоговаяНакладная.Номер);
		лДокОбъект = лВыборкаНН.НалоговаяНакладная.ПолучитьОбъект();
		лДокОбъект.Оплаты.Очистить();
		лВыборкаПоСтрокам = лВыборкаНН.Выбрать();
		Пока лВыборкаПоСтрокам.Следующий() Цикл
			лНоваяСтрока = лДокОбъект.Оплаты.Добавить();
			ЗаполнитьЗначенияСвойств(лНоваяСтрока, лВыборкаПоСтрокам);
		КонецЦикла;
		лДокОбъект.ОбменДанными.Загрузка = Истина;
		лДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьОплаты4

// ЗаполнитьОплаты3
//
Процедура ЗаполнитьОплаты3()
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("ДатаВПериоде", ДатаВПериоде);
	лЗапрос.УстановитьПараметр("СсылкаВходящаяНН", Документы.РегистрацияВходящегоНалоговогоДокумента.ПустаяСсылка());
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка,
	|	Док.Ссылка.Контрагент КАК Контрагент,
	|	Док.ДоговорКонтрагента,
	|	Док.Ссылка.Сделка КАК Сделка,
	|	СУММА(Док.Сумма + Док.СуммаНДС) КАК Сумма
	|ПОМЕСТИТЬ НН
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Товары КАК Док
	|ГДЕ
	|	Док.ДоговорКонтрагента.ВедениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоРасчетнымДокументам)
	|	И Док.Ссылка.Дата >= НАЧАЛОПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И Док.Ссылка.Дата <= КОНЕЦПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И (Док.Ссылка = &СсылкаВходящаяНН
	|			ИЛИ &СсылкаВходящаяНН = ЗНАЧЕНИЕ(Документ.РегистрацияВходящегоНалоговогоДокумента.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	Док.Ссылка,
	|	Док.Ссылка.Контрагент,
	|	Док.ДоговорКонтрагента,
	|	Док.Ссылка.Сделка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Рег.Регистратор КАК Платеж,
	|	Рег.СубконтоДт1 КАК Контрагент,
	|	Рег.СубконтоДт2 КАК ДоговорКонтрагента,
	|	Рег.СубконтоДт3 КАК Сделка,
	|	СУММА(Рег.СуммаОборот) КАК Сумма
	|ПОМЕСТИТЬ Платежи
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			,
	|			,
	|			Регистратор,
	|			СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСОтечественнымиПоставщиками)
	|				ИЛИ СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыданнымАвансамВНациональнойВалюте),
	|			,
	|			СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТекущиеСчетаВНациональнойВалюте),
	|			,
	|			СубконтоДт1 В
	|					(ВЫБРАТЬ
	|						НН.Контрагент
	|					ИЗ
	|						НН КАК НН)
	|				И СубконтоДт2 В
	|					(ВЫБРАТЬ
	|						НН.ДоговорКонтрагента
	|					ИЗ
	|						НН КАК НН)
	|				И СубконтоДт3 В
	|					(ВЫБРАТЬ
	|						НН.Сделка
	|					ИЗ
	|						НН КАК НН)) КАК Рег
	|
	|СГРУППИРОВАТЬ ПО
	|	Рег.Регистратор,
	|	Рег.СубконтоДт1,
	|	Рег.СубконтоДт2,
	|	Рег.СубконтоДт3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НН.Ссылка,
	|	Платежи.Платеж,
	|	НН.ДоговорКонтрагента,
	|	СУММА(ВЫБОР
	|			КОГДА НН.Сумма < Платежи.Сумма
	|				ТОГДА НН.Сумма
	|			ИНАЧЕ Платежи.Сумма
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ Врем
	|ИЗ
	|	НН КАК НН
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Платежи КАК Платежи
	|		ПО НН.Контрагент = Платежи.Контрагент
	|			И НН.ДоговорКонтрагента = Платежи.ДоговорКонтрагента
	|			И НН.Сделка = Платежи.Сделка
	|
	|СГРУППИРОВАТЬ ПО
	|	НН.Ссылка,
	|	Платежи.Платеж,
	|	НН.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Врем.Ссылка КАК НалоговаяНакладная,
	|	Врем.Платеж КАК ПлатежноеПоручение,
	|	Врем.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Врем.Сумма КАК Сумма,
	|	Врем.Ссылка.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Врем КАК Врем
	|ГДЕ
	|	Врем.Сумма > 0
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	НалоговаяНакладная
	|";
	лВыборкаНН = лЗапрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Пока лВыборкаНН.Следующий() Цикл
		ОбработкаПрерыванияПользователя();
		Состояние( "03. Обрабатывается НН № " + лВыборкаНН.НалоговаяНакладная.Номер);
		лДокОбъект = лВыборкаНН.НалоговаяНакладная.ПолучитьОбъект();
		лДокОбъект.Оплаты.Очистить();
		лВыборкаПоСтрокам = лВыборкаНН.Выбрать();
		Пока лВыборкаПоСтрокам.Следующий() Цикл
			лНоваяСтрока = лДокОбъект.Оплаты.Добавить();
			ЗаполнитьЗначенияСвойств(лНоваяСтрока, лВыборкаПоСтрокам);
		КонецЦикла;
		лДокОбъект.ОбменДанными.Загрузка = Истина;
		лДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьОплаты3

// ЗаполнитьОплаты2
//
Процедура ЗаполнитьОплаты2()
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("ДатаВПериоде", ДатаВПериоде);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	ДокТЧ_Платежи.ДоговорКонтрагента,
	|	ДокТЧ_Платежи.Ссылка КАК Платеж,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДокТЧ_Платежи.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ДокТЧ_Платежи.Сделка = ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ДокТЧ_Платежи.Сделка
	|		КОГДА ТИПЗНАЧЕНИЯ(ДокТЧ_Платежи.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ДокТЧ_Платежи.Ссылка.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ДокТЧ_Платежи.Ссылка.ДокументОснование
	|	КОНЕЦ КАК Сделка,
	|	ДокТЧ_Платежи.НалоговоеНазначение,
	|	ДокТЧ_Платежи.СтавкаНДС,
	|	СУММА(ДокТЧ_Платежи.СуммаПлатежа) КАК СуммаВсего
	|ПОМЕСТИТЬ ПлатежиПоПриходам
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее.РасшифровкаПлатежа КАК ДокТЧ_Платежи
	|ГДЕ
	|	ДокТЧ_Платежи.Ссылка.Проведен
	|	И (ТИПЗНАЧЕНИЯ(ДокТЧ_Платежи.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДокТЧ_Платежи.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокТЧ_Платежи.Ссылка,
	|	ДокТЧ_Платежи.ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДокТЧ_Платежи.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ДокТЧ_Платежи.Сделка = ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ДокТЧ_Платежи.Сделка
	|		КОГДА ТИПЗНАЧЕНИЯ(ДокТЧ_Платежи.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ДокТЧ_Платежи.Ссылка.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ДокТЧ_Платежи.Ссылка.ДокументОснование
	|	КОНЕЦ,
	|	ДокТЧ_Платежи.НалоговоеНазначение,
	|	ДокТЧ_Платежи.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НН.ДоговорКонтрагента,
	|	НН.Ссылка КАК НалоговаяНакладная,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоСделке.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоСделке.Сделка
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоПриходам.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоПриходам.Сделка
	|	КОНЕЦ КАК Сделка,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоСделке.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоСделке.Платеж
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоПриходам.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоПриходам.Платеж
	|	КОНЕЦ КАК ПлатежноеПоручение,
	|	ВЫБОР
	|		КОГДА НН.ПропорциональныйНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально)
	|		КОГДА НН.ДляОперацийОблагаемыхНДС
	|				И НН.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|		КОГДА НЕ НН.ДляОперацийОблагаемыхНДС
	|				И НН.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность)
	|		КОГДА НЕ НН.ДляОперацийОблагаемыхНДС
	|				И НЕ НН.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|	КОНЕЦ КАК НалоговоеНазначение,
	|	НН.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоСделке.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоСделке.СуммаВсего
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоПриходам.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоПриходам.СуммаВсего
	|	КОНЕЦ КАК СуммаВсегоПоПлатежу,
	|	СУММА(НН.СуммаНДС) КАК СуммаНДС,
	|	СУММА(НН.Сумма + НН.СуммаНДС) КАК СуммаВсего
	|ПОМЕСТИТЬ Платежи1
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Товары КАК НН
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПлатежиПоПриходам КАК ПлатежиПоСделке
	|		ПО (ИСТИНА)
	|			И НН.ДоговорКонтрагента = ПлатежиПоСделке.ДоговорКонтрагента
	|			И НН.СтавкаНДС = ПлатежиПоСделке.СтавкаНДС
	|			И НН.Ссылка.Сделка = ПлатежиПоСделке.Сделка
	|			И (НЕ НН.Ссылка.Сделка = НЕОПРЕДЕЛЕНО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПлатежиПоПриходам КАК ПлатежиПоПриходам
	|		ПО (ИСТИНА)
	|			И НН.ДоговорКонтрагента = ПлатежиПоПриходам.ДоговорКонтрагента
	|			И НН.СтавкаНДС = ПлатежиПоПриходам.СтавкаНДС
	|			И НН.Ссылка.ДокументОснование = ПлатежиПоПриходам.Сделка
	|			И (НЕ НН.Ссылка.ДокументОснование = НЕОПРЕДЕЛЕНО)
	|ГДЕ
	|	НН.Ссылка.Проведен
	|	И НН.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная)
	|	И НН.Ссылка.Дата >= НАЧАЛОПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И НН.Ссылка.Дата <= КОНЕЦПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И (ТИПЗНАЧЕНИЯ(НН.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|			ИЛИ ТИПЗНАЧЕНИЯ(НН.Ссылка.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг))
	|
	|СГРУППИРОВАТЬ ПО
	|	НН.Ссылка,
	|	НН.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НН.ПропорциональныйНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально)
	|		КОГДА НН.ДляОперацийОблагаемыхНДС
	|				И НН.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|		КОГДА НЕ НН.ДляОперацийОблагаемыхНДС
	|				И НН.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность)
	|		КОГДА НЕ НН.ДляОперацийОблагаемыхНДС
	|				И НЕ НН.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|	КОНЕЦ,
	|	НН.ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоСделке.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоСделке.Сделка
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоПриходам.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоПриходам.Сделка
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоСделке.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоСделке.Платеж
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоПриходам.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоПриходам.Платеж
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.Сделка) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоСделке.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоСделке.СуммаВсего
	|		КОГДА ТИПЗНАЧЕНИЯ(НН.Ссылка.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|				И НЕ ПлатежиПоПриходам.Платеж ЕСТЬ NULL 
	|			ТОГДА ПлатежиПоПриходам.СуммаВсего
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Платежи1.НалоговаяНакладная КАК НалоговаяНакладная,
	|	Платежи1.ДоговорКонтрагента,
	|	Платежи1.ПлатежноеПоручение,
	|	Платежи1.СуммаВсего КАК Сумма
	|ПОМЕСТИТЬ ОплатыВсе
	|ИЗ
	|	Платежи1 КАК Платежи1
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Платежи1.ПлатежноеПоручение) = ТИП(Документ.ПлатежноеПоручениеИсходящее)
	|	И НЕ Платежи1.ПлатежноеПоручение = ЗНАЧЕНИЕ(документ.ПлатежноеПоручениеИсходящее.ПустаяСсылка)
	|	И Платежи1.СуммаВсегоПоПлатежу = Платежи1.СуммаВсего
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатыВсе.НалоговаяНакладная,
	|	ОплатыВсе.ДоговорКонтрагента,
	|	ОплатыВсе.ПлатежноеПоручение
	|ПОМЕСТИТЬ ОплатыНужные
	|ИЗ
	|	ОплатыВсе КАК ОплатыВсе
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияВходящегоНалоговогоДокумента.Оплаты КАК ОплатыТЧ
	|		ПО ОплатыВсе.НалоговаяНакладная = ОплатыТЧ.Ссылка
	|			И ОплатыВсе.ДоговорКонтрагента = ОплатыТЧ.ДоговорКонтрагента
	|			И ОплатыВсе.ПлатежноеПоручение = ОплатыТЧ.ПлатежноеПоручение
	|			И ОплатыВсе.Сумма = ОплатыТЧ.Сумма
	|ГДЕ
	|	ОплатыТЧ.Сумма ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплатыВсе.НалоговаяНакладная,
	|	ОплатыВсе.ДоговорКонтрагента,
	|	ОплатыВсе.ПлатежноеПоручение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатыВсе.НалоговаяНакладная КАК НалоговаяНакладная,
	|	ОплатыВсе.ДоговорКонтрагента,
	|	ОплатыВсе.ПлатежноеПоручение,
	|	ОплатыВсе.Сумма КАК Сумма
	|ИЗ
	|	ОплатыВсе КАК ОплатыВсе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОплатыНужные КАК ОплатыНужные
	|		ПО ОплатыВсе.НалоговаяНакладная = ОплатыНужные.НалоговаяНакладная
	|			И ОплатыВсе.ДоговорКонтрагента = ОплатыНужные.ДоговорКонтрагента
	|			И ОплатыВсе.ПлатежноеПоручение = ОплатыНужные.ПлатежноеПоручение
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплатыВсе.НалоговаяНакладная,
	|	ОплатыВсе.ДоговорКонтрагента,
	|	ОплатыВсе.ПлатежноеПоручение,
	|	ОплатыВсе.Сумма
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОплатыВсе.НалоговаяНакладная
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	НалоговаяНакладная
	|";
	лВыборкаНН = лЗапрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "НалоговаяНакладная");
	Пока лВыборкаНН.Следующий() Цикл
		ОбработкаПрерыванияПользователя();
		Состояние( "02. Обрабатывается НН № " + лВыборкаНН.НалоговаяНакладная.Номер);
		лДокОбъект = лВыборкаНН.НалоговаяНакладная.ПолучитьОбъект();
		лДокОбъект.Оплаты.Очистить();
		лВыборкаПоСтрокам = лВыборкаНН.Выбрать();
		Пока лВыборкаПоСтрокам.Следующий() Цикл
			лНоваяСтрока = лДокОбъект.Оплаты.Добавить();
			ЗаполнитьЗначенияСвойств(лНоваяСтрока, лВыборкаПоСтрокам);
		КонецЦикла;
		лДокОбъект.ОбменДанными.Загрузка = Истина;
		лДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьОплаты2

// ЗаполнитьОплаты1
//
Процедура ЗаполнитьОплаты1()
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("ДатаВПериоде", ДатаВПериоде);
	лЗапрос.Текст =
	"ВЫБРАТЬ
	|	ДокТЧ.ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ДокТЧ.ПропорциональныйНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально)
	|		КОГДА ДокТЧ.ДляОперацийОблагаемыхНДС
	|				И ДокТЧ.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|		КОГДА НЕ ДокТЧ.ДляОперацийОблагаемыхНДС
	|				И ДокТЧ.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность)
	|		КОГДА НЕ ДокТЧ.ДляОперацийОблагаемыхНДС
	|				И НЕ ДокТЧ.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|	КОНЕЦ КАК НалоговоеНазначение,
	|	ДокТЧ.СтавкаНДС,
	|	ДокТЧ.Ссылка КАК НалоговаяНакладная,
	|	ВЫБОР
	|		КОГДА НЕ ДокТЧ.Ссылка.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПлатежноеПоручениеИсходящее.ПустаяСсылка)
	|			ТОГДА ДокТЧ.Ссылка.ДокументОснование
	|		ИНАЧЕ ДокТЧ.Ссылка.Сделка
	|	КОНЕЦ КАК Платеж,
	|	СУММА(ДокТЧ.Сумма) КАК Сумма,
	|	СУММА(ДокТЧ.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ДокТЧ.Сумма + ДокТЧ.СуммаНДС) КАК СуммаВсего
	|ПОМЕСТИТЬ Налоговые
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента.Товары КАК ДокТЧ
	|ГДЕ
	|	ДокТЧ.Ссылка.Проведен
	|	И ДокТЧ.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|	И (ТИПЗНАЧЕНИЯ(ДокТЧ.Ссылка.ДокументОснование) = ТИП(Документ.ПлатежноеПоручениеИсходящее)
	|				И НЕ ДокТЧ.Ссылка.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПлатежноеПоручениеИсходящее.ПустаяСсылка)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДокТЧ.Ссылка.Сделка) = ТИП(Документ.ПлатежноеПоручениеИсходящее)
	|				И НЕ ДокТЧ.Ссылка.Сделка = ЗНАЧЕНИЕ(Документ.ПлатежноеПоручениеИсходящее.ПустаяСсылка))
	|	И ДокТЧ.Ссылка.Дата >= НАЧАЛОПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|	И ДокТЧ.Ссылка.Дата <= КОНЕЦПЕРИОДА(&ДатаВПериоде, МЕСЯЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокТЧ.ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ДокТЧ.ПропорциональныйНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Пропорционально)
	|		КОГДА ДокТЧ.ДляОперацийОблагаемыхНДС
	|				И ДокТЧ.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|		КОГДА НЕ ДокТЧ.ДляОперацийОблагаемыхНДС
	|				И ДокТЧ.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность)
	|		КОГДА НЕ ДокТЧ.ДляОперацийОблагаемыхНДС
	|				И НЕ ДокТЧ.ДляХозяйственнойДеятельности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|	КОНЕЦ,
	|	ДокТЧ.СтавкаНДС,
	|	ДокТЧ.Ссылка,
	|	ВЫБОР
	|		КОГДА НЕ ДокТЧ.Ссылка.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПлатежноеПоручениеИсходящее.ПустаяСсылка)
	|			ТОГДА ДокТЧ.Ссылка.ДокументОснование
	|		ИНАЧЕ ДокТЧ.Ссылка.Сделка
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокТЧ.ДоговорКонтрагента,
	|	ДокТЧ.НалоговоеНазначение,
	|	ДокТЧ.СтавкаНДС,
	|	ДокТЧ.Ссылка КАК Платеж,
	|	СУММА(ДокТЧ.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ДокТЧ.СуммаПлатежа) КАК СуммаВсего
	|ПОМЕСТИТЬ Платежи
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее.РасшифровкаПлатежа КАК ДокТЧ
	|ГДЕ
	|	ДокТЧ.Ссылка В
	|			(ВЫБРАТЬ
	|				Налоговые.Платеж
	|			ИЗ
	|				Налоговые КАК Налоговые)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокТЧ.ДоговорКонтрагента,
	|	ДокТЧ.НалоговоеНазначение,
	|	ДокТЧ.СтавкаНДС,
	|	ДокТЧ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Налоговые.НалоговаяНакладная,
	|	Налоговые.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Налоговые.Платеж КАК ПлатежноеПоручение,
	|	Налоговые.СуммаВсего КАК Сумма
	|ПОМЕСТИТЬ ВсеНалоговые
	|ИЗ
	|	Налоговые КАК Налоговые
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Платежи КАК Платежи
	|		ПО Налоговые.ДоговорКонтрагента = Платежи.ДоговорКонтрагента
	|			И Налоговые.НалоговоеНазначение = Платежи.НалоговоеНазначение
	|			И Налоговые.СтавкаНДС = Платежи.СтавкаНДС
	|			И Налоговые.Платеж = Платежи.Платеж
	|			И Налоговые.СуммаНДС = Платежи.СуммаНДС
	|			И Налоговые.СуммаВсего = Платежи.СуммаВсего
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеНалоговые.НалоговаяНакладная,
	|	ВсеНалоговые.ДоговорКонтрагента,
	|	ВсеНалоговые.ПлатежноеПоручение
	|ПОМЕСТИТЬ НужныеНалоговые
	|ИЗ
	|	ВсеНалоговые КАК ВсеНалоговые
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияВходящегоНалоговогоДокумента.Оплаты КАК ОплатыТЧ
	|		ПО ВсеНалоговые.НалоговаяНакладная = ОплатыТЧ.Ссылка
	|			И ВсеНалоговые.ДоговорКонтрагента = ОплатыТЧ.ДоговорКонтрагента
	|			И ВсеНалоговые.ПлатежноеПоручение = ОплатыТЧ.ПлатежноеПоручение
	|			И ВсеНалоговые.Сумма = ОплатыТЧ.Сумма
	|ГДЕ
	|	ОплатыТЧ.Сумма ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеНалоговые.НалоговаяНакладная КАК НалоговаяНакладная,
	|	ВсеНалоговые.ДоговорКонтрагента,
	|	ВсеНалоговые.ПлатежноеПоручение,
	|	ВсеНалоговые.Сумма КАК Сумма
	|ИЗ
	|	ВсеНалоговые КАК ВсеНалоговые
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НужныеНалоговые КАК НужныеНалоговые
	|		ПО ВсеНалоговые.НалоговаяНакладная = НужныеНалоговые.НалоговаяНакладная
	|			И ВсеНалоговые.ДоговорКонтрагента = НужныеНалоговые.ДоговорКонтрагента
	|			И ВсеНалоговые.ПлатежноеПоручение = НужныеНалоговые.ПлатежноеПоручение
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеНалоговые.ПлатежноеПоручение,
	|	ВсеНалоговые.НалоговаяНакладная,
	|	ВсеНалоговые.ДоговорКонтрагента,
	|	ВсеНалоговые.Сумма
	|
	|УПОРЯДОЧИТЬ ПО
	|	НалоговаяНакладная
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	НалоговаяНакладная
	|";
	лВыборкаНН = лЗапрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "НалоговаяНакладная");
	Пока лВыборкаНН.Следующий() Цикл
		ОбработкаПрерыванияПользователя();
		Состояние( "01. Обрабатывается НН № " + лВыборкаНН.НалоговаяНакладная.Номер);
		лДокОбъект = лВыборкаНН.НалоговаяНакладная.ПолучитьОбъект();
		лДокОбъект.Оплаты.Очистить();
		лВыборкаПоСтрокам = лВыборкаНН.Выбрать();
		Пока лВыборкаПоСтрокам.Следующий() Цикл
			лНоваяСтрока = лДокОбъект.Оплаты.Добавить();
			ЗаполнитьЗначенияСвойств(лНоваяСтрока, лВыборкаПоСтрокам);
		КонецЦикла;
		лДокОбъект.ОбменДанными.Загрузка = Истина;
		лДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьОплаты1

// УстановитьОтбор
//
Процедура УстановитьОтбор(КомпоновщикНастроек, ИмяПараметра, ЗначениеПараметра)
	ЭлементОтбора = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПараметра);
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
	Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ЗначениеПараметра;	
		Попытка
			лЭтоГруппа = ЗначениеПараметра.ЭтоГруппа;
			Если лЭтоГруппа Тогда
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
			Иначе
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		Исключение
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецПопытки;
	Иначе
		ЭлементОтбора.Использование = Ложь;
	КонецЕсли;
КонецПроцедуры

// УстановитьПараметр
//
Процедура УстановитьПараметр(КомпоновщикНастроек, ИмяПараметра, ЗначениеПараметра)
	ПараметрДанныхТекущий = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	ПараметрДанныхТекущий.Значение = ЗначениеПараметра;
	ПараметрДанныхТекущий.Использование = Истина;
КонецПроцедуры

Процедура УстановитьПараметрыОтборы()
	УстановитьПараметр(КомпоновщикНастроек, "ДатаВПериоде", ДатаВПериоде );
	УстановитьПараметр(КомпоновщикНастроек, "ТолькоНезаполненныеОплаты", ТолькоНезаполненныеОплаты );
	
	КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();
	УстановитьОтбор(КомпоновщикНастроек, "СтавкаНДС", СтавкаНДС );
КонецПроцедуры

Процедура ПриОткрытии()
	Если Не ЗначениеЗаполнено(ДатаВПериоде) Тогда ДатаВПериоде = НачалоМесяца( ТекущаяДата() ) - 1; КонецЕсли;
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура ДатаВПериодеПриИзменении(Элемент)
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура ТолькоНезаполненныеОплатыПриИзменении(Элемент)
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура ЗаполнитьОплаты(Кнопка)
	// Заполним оплаты для НН с полем ДокументОснование = ПлатежноеПоручение
	ЗаполнитьОплаты1();
	// Заполним оплаты для НН с полем ДокументОснование = ПриходнаяНакладная
	ЗаполнитьОплаты2();
	// Заполним оплаты для НН по аналитике субконто3 "Рачетный докуумент" по счету631
	ЗаполнитьОплаты3();
	// Заполним оплаты для НН выписанным по Приходам по договорам с ведением расчетов в целом по договору
	ЗаполнитьОплаты4();
КонецПроцедуры

Процедура СтавкаНДСПриИзменении(Элемент)
	УстановитьПараметрыОтборы();
КонецПроцедуры

Процедура ТабличноеПоле1ДокументОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	лДокОбъект = Элемент.Значение.ПолучитьОбъект();
	лФорма = лДокОбъект.ПолучитьФорму("ФормаДокументаДляПлатежей", ЭтаФорма);
	лФорма.Открыть();
КонецПроцедуры

Процедура Панель1ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.Страница2 Тогда
		СписокНН.Очистить();
		Если Не СокрЛП(ЭлементыФормы.Результат.ПолучитьОбласть( "R1C4" ).ТекущаяОбласть.Текст) = "НН Номер" Тогда
			Сообщить("Колонка № 4 должна иметь заголовок : 'НН Номер' !" );
			Возврат;
		КонецЕсли;
		Если Не СокрЛП(ЭлементыФормы.Результат.ПолучитьОбласть( "R1C5" ).ТекущаяОбласть.Текст) = "НН Дата" Тогда
			Сообщить("Колонка № 4 должна иметь заголовок : 'НН Дата' !" );
			Возврат;
		КонецЕсли;
		Для лНомСтроки=2 По 1000 Цикл
			лНомерНН = СокрЛП(ЭлементыФормы.Результат.ПолучитьОбласть( "R" + Формат(лНомСтроки, "ЧГ=0") + "C4" ).ТекущаяОбласть.Текст);
			лДатаННТекст = СокрЛП(ЭлементыФормы.Результат.ПолучитьОбласть( "R" + Формат(лНомСтроки, "ЧГ=0") + "C5" ).ТекущаяОбласть.Текст);
			Если Не ЗначениеЗаполнено(лНомерНН) Тогда Прервать; КонецЕсли;
			Попытка
				лДатаНН = Дата( Число(Прав(лДатаННТекст,4)), Число(Сред(лДатаННТекст,4,2)), Число(лев(лДатаННТекст,2)));
			Исключение
				лДатаНН = ДатаВПериоде;
			КонецПопытки;
			// Ищем налоговую накладную по номеру
			лННТут = Документы.РегистрацияВходящегоНалоговогоДокумента.НайтиПоНомеру(лНомерНН, лДатаНН);
			Если ЗначениеЗаполнено(лННТут) Тогда
				лНоваяСтрока = СписокНН.Добавить();
				лНоваяСтрока.Документ = лННТут;
				лНоваяСтрока.СуммаНН = лННТут.Товары.Итог("Сумма") + лННТут.Товары.Итог("СуммаНДС");
				лНоваяСтрока.СуммаОплачено = лННТут.Оплаты.Итог("Сумма");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ТабличноеПоле1ПриАктивизацииСтроки(Элемент)
	Если (Не Элемент.ТекущаяСтрока = Неопределено) и ЗначениеЗаполнено(Элемент.ТекущаяСтрока.Документ) Тогда
		ДокументТут = Элемент.ТекущаяСтрока.Документ;
	КонецЕсли;
КонецПроцедуры

Процедура ДокументОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДокументТут) Тогда Возврат; КонецЕсли;
	
	лДокОбъект = ДокументТут.ПолучитьОбъект();
	лФорма = лДокОбъект.ПолучитьФорму("ФормаДокументаДляПлатежей", ЭтаФорма);
	лФорма.Открыть();
КонецПроцедуры
